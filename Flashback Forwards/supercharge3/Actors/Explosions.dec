Actor TangoExplosionSpawner : SwitchingDecoration 26401
{
	var int user_loops_done;
	+NOGRAVITY
	Radius 16
	Height 32
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_SetUserVar("user_loops_done", 0)
		TNT1 A -1
		Stop
	Active:
	RandomizeTics:
		TNT1 AAAA 8 A_Jump(100, "Explode")
		Goto Explode
	Explode:
		TNT1 A 0 A_SpawnItemEx("TangoExplosion1", 0, 0, 0, random(-6, 6), random(-6, 6), random(5, 12), 0)
		TNT1 A 0 A_SetUserVar("user_loops_done", user_loops_done + 1)
		TNT1 A 0 A_JumpIf(user_loops_done == 14, "End")
		Goto RandomizeTics
	End:
		TNT1 A 0
		Stop
	}
}

Actor TangoExplosion1
{
	// Scale 0.15
	Scale 0.75
	Radius 16
	Height 32
	BounceType Doom
	Gravity 0.6
	+THRUACTORS
	+CLIENTSIDEONLY
	+DONTSPLASH
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("Stomper/explode")
		XPL1 AAAAABCD 2 Bright A_SpawnItemEx("TangoExplosion1Trail")
		Stop
	}
}

Actor TangoExplosion1Trail
{
	// Scale 0.15
	Scale 0.75
	+NOGRAVITY
	+CLIENTSIDEONLY
	+NOINTERACTION
	+DONTSPLASH
	States
	{
	Spawn:
		XPL1 ABCD 3 Bright
		Stop
	}
}