const float agathodemon_crit_multiplier = 1.8;

ACTOR TangoAgathodemon // 27704
{
	//$Category "Monsters"
	//$Color 12
	//$Title "TangoAgathodemon"
	Tag "Agathodemon"
	Health 360
	Radius 31
	Height 56
	Mass 600
	Speed 12
	PainChance 96
	PainChance "Chaingun", 20
	PainChance "FireTrap", 180
	Monster
	+FLOAT
	+NOGRAVITY
	+DONTHARMSPECIES
	Species "Caco"
	SeeSound "agathodemon/see"
	PainSound "agathodemon/pain"
	DeathSound "caco/death"
	ActiveSound "caco/active"
	Obituary "%o was destroyed by an Agathodemon."
	HitObituary "%o was mauled by an Agathodemon."
	States
	{
	Spawn:
		AGAH A 10 A_Look
		Loop
	See:
		AGAH A 3 A_Chase
		Loop
	Melee:
		AGAH C 4 A_FaceTarget
		AGAH DC 5 A_FaceTarget
		AGAH B 6 A_CustomMeleeAttack(30, "agathodemon/melee")
		Goto See
	Missile:
		AGAH BC 5 A_FaceTarget
		TNT1 A 0 A_CustomMissile("CacodemonVortexBall", 32, 0, -5)
		TNT1 A 0 A_CustomMissile("CacodemonVortexBall", 32, 0, 0)
		TNT1 A 0 A_CustomMissile("CacodemonVortexBall", 32, 0, 5)
		AGAH D 5 Bright
		Goto See
	Pain:
		AGAH E 3
		AGAH E 3 A_Pain
		AGAH F 6
		Goto See
	Raise:
		AGAH L 6 A_UnSetFloorClip
		AGAH KJIHG 6
		Goto See
	Death.Massacre:
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		AGAH L -1 A_SetFloorClip
		Stop
		
	// ****************************
	// DAMAGE TYPE DEATH SWITCHES
	// ****************************
	Death:
		Goto RegularDeath
	
	Death.Rocket:
		Goto ExplodeDeath
		
	XDeath:
		Goto ExplodeDeath
		
	Death.Shotgun:
		TNT1 A 0 A_JumpIfCloser(200, "DeathRandom1")
		Goto RegularDeath

	DeathRandom1:
		TNT1 A 0 A_Jump(120, "ExplodeDeath")
		Goto RegularDeath
	
	// ****************************
	// DEATH ANIMATIONS
	// ****************************
	RegularDeath:
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_SpawnItemEx("GenericXDeathSpawner2", 0, 0, 0, 0, 0, 0, angle*256/360+128)
		AGAH G 5
		AGAH H 5
		AGAH IJ 5
		AGAH K 5
		AGAH L -1 A_SetFloorClip
		Stop
	
	ExplodeDeath:
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_SpawnItemEx("GenericXDeathSpawner1")
		AGD2 A 2
		AGD2 BCDEFGHI 2 A_SpawnItemEx("NewFlyingBlood",0,0,24,Random(5,-5),Random(5,-5),Random(3,6),0,129)
		AGD2 J -1 A_SetFloorClip
		Stop
  }
}