Actor TangoPlasmaZombieDropsClip replaces ZombieMan
{
	//$Category Monsters
	//$Color 12
	//$Title "TangoPlasmaZombie (Drops Half Clip)"

	Tag "Plasma Zombie"
	Health 40
	Radius 20
	Height 56
	Mass 100
	Speed 8
	PainChance 190
	PainChance "Chaingun", 100
	PainChance "FireTrap", 180
	Monster
	+FLOORCLIP
	+MISSILEMORE
	seesound "grunt/sight"
	attacksound "grunt/attack"
	painsound "grunt/pain"
	deathsound "grunt/death"
	activesound "grunt/active"
	obituary "%o was cauterised by a zombie's plasma."
	  
	States
	{

	Spawn:
		ZBAM A 10 A_Look
		Loop
	See:
		ZBAM AABBCCDD 3 A_Chase
		Loop
	Missile:
		ZBAM E 11 A_FaceTarget
		ZBAM F 4 Bright A_CustomMissile("PlasmaZombiePlasmaBall", 32, 8)
		ZBAM E 2 A_FaceTarget
		ZBAM F 4 Bright A_CustomMissile("PlasmaZombiePlasmaBall", 32, 8)
		ZBAM E 10
		Goto See
	Pain:
		ZBAM G 3
		ZBAM G 3 A_Pain
		Goto See
	Raise:
		ZBAM L 4
		ZBAM KJIH 4
		Goto See
	Death.Massacre:
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		ZBAM L -1
		Stop
	
	// ****************************
	// DAMAGE TYPE DEATH SWITCHES
	// ****************************
	Death:
		TNT1 A 0 A_SpawnItemEx("TangoBulletClipHalf", 0, 0, 16, 0, 0, 0)
		Goto RegularDeath
		
	Death.Bullet:
	Death.Chaingun:
		TNT1 A 0 A_SpawnItemEx("TangoBulletClipHalf", 0, 0, 16, 0, 0, 0)
		TNT1 A 0 A_Jump(140, "HeadDeath")
		Goto RegularDeath

	Death.Shotgun:
	Death.Plasma:
		TNT1 A 0 A_SpawnItemEx("TangoBulletClipHalf", 0, 0, 16, 0, 0, 0)
		TNT1 A 0 A_JumpIfCloser(200, "DeathRandom1")
		Goto RegularDeath
	
	Death.Rocket:
		TNT1 A 0 A_SpawnItemEx("TangoBulletClipHalf", 0, 0, 16, 0, 0, 0)
		Goto LegsDeath
	
	XDeath:
		TNT1 A 0 A_SpawnItemEx("TangoBulletClipHalf", 0, 0, 16, 0, 0, 0)
		Goto LegsDeath
		
	DeathRandom1:
		TNT1 A 0 A_Jump(200, "FlyDeath")
		TNT1 A 0 ThrustThing(angle*256/360+128, 2)
		TNT1 A 0 ThrustThingZ(0, 6, 0, 1)
		Goto LegsDeath
	
		
	// ****************************
	// DEATH ANIMATIONS
	// ****************************
	RegularDeath:
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_NoBlocking
		TNT1 AAAAA 0 A_SpawnItemEx("NewFlyingBlood",0,0,48,Random(5,-5),Random(5,-5),Random(3,6),0,129)
		ZBAM H 5
		ZBAM I 5 A_Scream
		ZBAM J 5 
		ZBAM K 5 A_Stop
		ZBAM L -1
		Stop
		
	HeadDeath:
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 ThrustThing(angle*256/360+128, 4)
		TNT1 A 0 ThrustThingZ(0, 6, 0, 1)
		TNT1 AAA 0 A_SpawnItemEx("NewFlyingBlood",0,0,48,Random(5,-5),Random(5,-5),Random(3,6),0,129)
		TNT1 A 0 A_SpawnItemEx("GenericXDeathSpawner6", 48)
		ZBD1 A 5
		TNT1 AAA 0 A_SpawnItemEx("NewFlyingBlood",0,0,32,Random(5,-5),Random(5,-5),Random(3,6),0,129)
		ZBD1 B 5
		ZBD1 C 6
		ZBD1 D 5 A_Stop
		ZBD1 E -1
		Stop

	FlyDeath:
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 ThrustThing(angle*256/360+128, 5)
		TNT1 A 0 ThrustThingZ(0, 32, 0, 1)
		TNT1 A 0 A_SpawnItemEx("GenericXDeathSpawner4")
		TNT1 AAA 0 A_SpawnItemEx("NewFlyingBlood",0,0,48,Random(5,-5),Random(5,-5),Random(3,6),0,129)
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		ZBD2 A 7
		ZBD2 B 7
	FlyDeathAirLoop:
		ZBD2 C 2 A_CheckFloor("FlyDeathFinish")
		Loop
	FlyDeathFinish:
		TNT1 A 0 A_Stop
		TNT1 AAA 0 A_SpawnItemEx("NewFlyingBlood",0,0,10,Random(5,-5),Random(5,-5),Random(3,6),0,129)
		TNT1 A 0 A_PlaySound("misc/xdeath")
		ZBAM L -1
		Stop
  
	LegsDeath:
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 ThrustThing(angle*256/360+128, 6)
		TNT1 A 0 ThrustThingZ(0, 16, 0, 3)
		TNT1 AAA 0 A_SpawnItemEx("NewFlyingBlood",0,0,10,Random(5,-5),Random(5,-5),Random(3,6),0,129)
		TNT1 A 0 A_SpawnItemEx("GenericXDeathSpawner1", 32, 0, 0, 0, 0, 0,0, 129)
		TNT1 A 0 A_XScream
		TNT1 A 0 A_NoBlocking
		ZBD3 A 1
	LegsDeathAirLoop:
		ZBD3 A 2 A_CheckFloor("LegsDeathFinish")
		Loop
	LegsDeathFinish:
		TNT1 AAA 0 A_SpawnItemEx("NewFlyingBlood",0,0,10,Random(5,-5),Random(5,-5),Random(3,6),0,129)
		TNT1 A 0 A_PlaySound("misc/xdeath")
		TNT1 A 0 A_Stop
		ZBD3 BC 5
		ZBD3 D -1
		Stop
	}
}

Actor TangoPlasmaZombie : TangoPlasmaZombieDropsClip 27706
{
	//$Category Monsters
	//$Color 12
	//$Title "TangoPlasmaZombie"
	Tag "Plasma Zombie"
	States
	{
	// Copied code of the death switches from the regular version of the monster,
	// but with all the lines to spawn half clip items removed
	// ****************************
	// DAMAGE TYPE DEATH SWITCHES
	// ****************************
	Death:
		Goto RegularDeath
		
	Death.Bullet:
	Death.Chaingun:
		TNT1 A 0 A_Jump(140, "HeadDeath")
		Goto RegularDeath

	Death.Shotgun:
	Death.Plasma:
		TNT1 A 0 A_JumpIfCloser(200, "DeathRandom1")
		Goto RegularDeath
	
	Death.Rocket:
		Goto LegsDeath
	
	XDeath:
		Goto LegsDeath
	}
}


ACTOR PlasmaZombiePlasmaBall
{
  Radius 3
  Height 6
  Speed 22
  Damage (8)
  Scale 0.6
  Projectile
  +RANDOMIZE
  RenderStyle Add
  Alpha 0.75
  SeeSound "weapons/plasmaf"
  DeathSound "weapons/hrballexplode"
  States
  {
  Spawn:
    VLSS AABBCCDDEEFFGGHH 1 Bright A_SpawnItemEx("PlasmaZombiePlasmaBallTrail")
    Loop
  Death:
    VLSE ABCDEFGHIJKL 2 Bright
    Stop
  }
}

Actor PlasmaZombiePlasmaBallTrail
{
	Radius 1
	Height 1
	Speed 0
	Alpha 0.6
	Scale 0.6
	+NOINTERACTION
	+CLIENTSIDEONLY
    States
    {
    Spawn:
		TNT1 A 3
	Death:
		VLSE JKL 1 Bright
		Stop
    }
}