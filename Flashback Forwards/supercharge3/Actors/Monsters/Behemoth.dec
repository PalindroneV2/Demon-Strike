// currently the behemoth has an extra death animation from Brutal Doom that's unused (death3, BED3 sprites)
ACTOR TangoBehemoth : Fatso replaces Fatso
{
	//$Category Monsters
	//$Color 12
	//$Title "TangoBehemoth"
	Tag "Behemoth"
	Health 600
	PainChance 30
	PainChance "Chaingun", 20
	PainChance "FireTrap", 180
	SeeSound "behemoth/see"
	PainSound "behemoth/pain"
	DeathSound "behemoth/death"
	Obituary "%o was squashed by a Behemoth."
	Species "Behemoth"
	+DONTHARMCLASS
	States
	{
	Spawn:
		BEHE C 8 A_Look
		BEHE C 8 A_Look
		BEHI C 8 A_Look
		BEHI C 8 A_Look
		Loop
	See:
		BEHE AABBCCDDEEFF 3 A_Chase
		Loop
	Missile:
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("tango_cvar_projectile_origin_behemoth") == 1, "VanillaOriginMissile")
		TNT1 A 0 A_PlaySound("behemoth/attack")
		BEHE G 15 A_FaceTarget
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_CustomMissile("BehemothComet", 24, 30, 0)
		BEHE H 6 A_CustomMissile("BehemothComet", 24, -30, 18)
		BEHE IG 6 A_FaceTarget
		TNT1 A 0 A_CustomMissile("BehemothComet", 24, 30, -18)
		BEHE H 6 A_CustomMissile("BehemothComet", 24, -30, 0)
		BEHE IG 6 A_FaceTarget
		TNT1 A 0 A_CustomMissile("BehemothComet", 24, 30, -12)
		BEHE H 6 A_CustomMissile("BehemothComet", 24, -30, 12)
		BEHE IG 12
		Goto See
	VanillaOriginMissile:
		TNT1 A 0 A_PlaySound("behemoth/attack")
		BEHE G 15 A_FaceTarget
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_CustomMissile("BehemothComet", 32, 0, 0)
		BEHE H 6 A_CustomMissile("BehemothComet", 32, 0, 18)
		BEHE IG 6 A_FaceTarget
		TNT1 A 0 A_CustomMissile("BehemothComet", 32, 0, -18)
		BEHE H 6 A_CustomMissile("BehemothComet", 32, 0, 0)
		BEHE IG 6 A_FaceTarget
		TNT1 A 0 A_CustomMissile("BehemothComet", 32, 0, -12)
		BEHE H 6 A_CustomMissile("BehemothComet", 32, 0, 12)
		BEHE IG 12
		Goto See
	Pain:
		BEHE J 6 A_Pain
		Goto See
	Raise:
		BEHE SRQPONMLK 6
		Goto See
	Death.Massacre:
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		BEHE T -1 A_BossDeath
		Stop
		
	
	// ****************************
	// DAMAGE TYPE DEATH SWITCHES
	// ****************************
	Death:
		Goto RegularDeath
		
	Death.Rocket:
		TNT1 A 0 A_Jump(256, "GapingDeath", "HeadlessDeath")
		Goto RegularDeath
		
	Death.Shotgun:
		TNT1 A 0 A_JumpIfCloser(200, "DeathRandom1")
		Goto Death
		
	DeathRandom1:
		TNT1 A 0 A_Jump(100, "GapingDeath", "HeadlessDeath")
		Goto RegularDeath
		
	
	// ****************************
	// DEATH ANIMATIONS
	// ****************************
	RegularDeath:
		TNT1 A 0 A_FaceTarget
		TNT1 AAAA 0 A_SpawnItemEx("NewFlyingBlood",0,0,random(16, 32),Random(5,-5),Random(5,-5),Random(3,6),0,129)
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		// If we're within a certain range, jump to the duration-less
		// states to cause the thrusting back and up
		TNT1 A 0 A_JumpIfCloser(128, "RegularDeathThrustUpBack")
		// Otherwise, just continue to the main part of the regular death
		Goto RegularDeathMain
		
	// I put this ThrustUpBack section in between here because if it happens,
	// this is chronologically where it'd be
	RegularDeathThrustUpBack:
		TNT1 A 0 ThrustThing(angle*256/360+128, 6)
		TNT1 A 0 ThrustThingZ(0, 16, 0, 1)
		// After we've thrusted, continue on to main part of regular death
		Goto RegularDeathMain
		
	RegularDeathMain:
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		BEHE K 6
		TNT1 AAAA 0 A_SpawnItemEx("NewFlyingBlood",0,0,random(16, 40),Random(5,-5),Random(5,-5),Random(3,6),0,129)
		BEHE L 6
		TNT1 AAAA 0 A_SpawnItemEx("NewFlyingBlood",0,0,random(16, 40),Random(5,-5),Random(5,-5),Random(3,6),0,129)
		BEHE M 6
		TNT1 AAAA 0 A_SpawnItemEx("NewFlyingBlood",0,0,random(16, 40),Random(5,-5),Random(5,-5),Random(3,6),0,129)
		BEHE N 6 A_SpawnItemEx("GenericXDeathSpawner4", 0, 0, 0, 0, 0, 0, angle*256/360+128)
		BEHE OPQR 6
		BEHE S 6
		BEHE T -1 A_BossDeath
		Stop
		
	
	GapingDeath:
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		TNT1 AAAAA 0 A_SpawnItemEx("NewFlyingBlood",0,0,52,Random(5,-5),Random(5,-5),Random(3,6),0,129)
		TNT1 A 0 A_SpawnItemEx("GenericXDeathSpawner1")
		BED4 A 20
		BED4 B 5
		BED4 CD 4
		BED4 E -1 A_BossDeath
		Stop
		
	
	HeadlessDeath:
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 ThrustThing(angle*256/360+128, 8)
		TNT1 A 0 ThrustThingZ(0, 24, 0, 1)
		TNT1 A 0 A_SpawnItemEx("GenericXDeathSpawner1")
		TNT1 AAAAA 0 A_SpawnItemEx("NewFlyingBlood",0,0,52,Random(5,-5),Random(5,-5),Random(3,6),0,129)
		BED2 A 7
		BED2 B 7
		BED2 C 6
	HeadlessDeathAirLoop:
		BED2 C 2 A_CheckFloor("HeadlessDeathFinish")
		Loop
	HeadlessDeathFinish:
		TNT1 AAAAA 0 A_SpawnItemEx("NewFlyingBlood",0,0,52,Random(5,-5),Random(5,-5),Random(3,6),0,129)
		BED2 D 3
		BED2 E -1 A_BossDeath
		Stop
	}
}

Actor TangoBehemothSlow : TangoBehemoth
{
	States
	{
	Missile:
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("tango_cvar_projectile_origin_behemoth") == 1, "VanillaOriginMissile")
		TNT1 A 0 A_PlaySound("behemoth/attack")
		BEHE G 15 A_FaceTarget
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_CustomMissile("BehemothCometSlow", 24, 30, 0)
		BEHE H 6 A_CustomMissile("BehemothCometSlow", 24, -30, 18)
		BEHE IG 9 A_FaceTarget
		TNT1 A 0 A_CustomMissile("BehemothCometSlow", 24, 30, -18)
		BEHE H 6 A_CustomMissile("BehemothCometSlow", 24, -30, 0)
		BEHE IG 9 A_FaceTarget
		TNT1 A 0 A_CustomMissile("BehemothCometSlow", 24, 30, -12)
		BEHE H 6 A_CustomMissile("BehemothCometSlow", 24, -30, 12)
		BEHE IG 18
		Goto See
	VanillaOriginMissile:
		TNT1 A 0 A_PlaySound("behemoth/attack")
		BEHE G 15 A_FaceTarget
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_CustomMissile("BehemothCometSlow", 32, 0, 0)
		BEHE H 6 A_CustomMissile("BehemothCometSlow", 32, 0, 18)
		BEHE IG 9 A_FaceTarget
		TNT1 A 0 A_CustomMissile("BehemothCometSlow", 32, 0, -18)
		BEHE H 6 A_CustomMissile("BehemothCometSlow", 32, 0, 0)
		BEHE IG 9 A_FaceTarget
		TNT1 A 0 A_CustomMissile("BehemothCometSlow", 32, 0, -12)
		BEHE H 6 A_CustomMissile("BehemothCometSlow", 32, 0, 12)
		BEHE IG 18
		Goto See
	}
}

Actor BehemothComet
{
  Radius 10
  Height 20
  Speed 20
  Damage (28)
  Projectile
  +RANDOMIZE
  SeeSound "fatso/attack"
  DeathSound "behemoth/cometdeath"
  States
  {
  Spawn:
    COME AAABBBCCC 1 Bright A_SpawnItemEx("BehemothCometTrail")
    Loop
  Death:
	TNT1 A 0 // A_Explode(8, 128)
    COME DEFGHI 3 Bright
    Stop
  }
}

Actor BehemothCometSlow : BehemothComet
{
	Speed 15
}

Actor BehemothCometTrail
{
	Radius 1
	Height 1
	Speed 0
	Alpha 0.6
	+NOGRAVITY
	+CLIENTSIDEONLY
	+NOINTERACTION
	States
	{
	Spawn:
		TNT1 A 3
	SpawnLoop:
		COMT ABCDEFGHI 1 Bright A_Fadeout(0.06)
		Stop
	}
}

ACTOR TangoHaedexebus 28007
{
	//$Category Monsters
	//$Color 12
	//$Title "TangoHaedexebus"
	Tag "Haedexebus"
	Height 96
	Radius 48
	Health 600
	PainChance 80
	Speed 10
	Mass 4000
	Species "Behemoth"
	Monster
	+FLOAT
	-FLOORCLIP
	+NOGRAVITY
	+DONTHURTSPECIES
	+DONTHARMSPECIES
	+DONTHARMCLASS
	SeeSound "behemoth/see"
	PainSound "behemoth/pain"
	DeathSound "behemoth/death"
	Obituary "%o was squashed by a Haedexebus."
	States
	{
	Spawn:
		HAED A 8 A_Look
		Loop
	See:
		HAED AA 3 A_Chase
		Loop
	Missile:
		TNT1 A 0 A_PlaySound("behemoth/attack")
		HAED B 15 A_FaceTarget
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_CustomMissile("BehemothComet", 40, 30, -1)
		HAED C 6 A_CustomMissile("BehemothComet", 40, -30, 1)
		HAED DB 6 A_FaceTarget
		TNT1 A 0 A_CustomMissile("BehemothComet", 40, 30, -6)
		HAED C 6 A_CustomMissile("BehemothComet", 40, -30, 6)
		HAED DB 8 A_FaceTarget
		TNT1 A 0 A_CustomMissile("BehemothComet", 40, 30, -12)
		HAED C 6 A_CustomMissile("BehemothComet", 40, -30, 12)
		HAED DB 12
		Goto See
	Pain:
		HAED E 6 A_Pain
		Goto See
	Death:
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_Scream
		HAED F 6
		TNT1 AAAA 0 A_SpawnItemEx("NewFlyingBlood",0,0,random(16, 40),Random(5,-5),Random(5,-5),Random(3,6),0,129)
		HAED G 6
		TNT1 AAAA 0 A_SpawnItemEx("NewFlyingBlood",0,0,random(16, 40),Random(5,-5),Random(5,-5),Random(3,6),0,129)
		HAED H 6
		TNT1 AAAA 0 A_SpawnItemEx("NewFlyingBlood",0,0,random(16, 40),Random(5,-5),Random(5,-5),Random(3,6),0,129)
		HAED I 6 A_SpawnItemEx("GenericXDeathSpawner4", 0, 0, 0, 0, 0, 0, angle*256/360+128)
		HAED JK 6
		HAED L -1
		Stop
	Raise:
		HAED LKJIHGF 6
		Goto See
	}
}