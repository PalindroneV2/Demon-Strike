Actor TangoWicked 27712
{ 
	//$Category Monsters
	//$Color 12
	//$Title "TangoWicked"
	Tag "Wicked"
	Health 200
	Radius 20
	Height 88
	Mass 200 
	Speed 12
	PainChance 112
	PainChance "FireTrap", 180
	Monster
	+NoGravity
	+Float
	+MISSILEMORE
	Obituary "%o got blasted by a Wicked."
	SeeSound "monster/wiksit" 
	PainSound "monster/wikpai" 
	DeathSound "monster/wikdth" 
	ActiveSound "monster/wikact" 
	States 
	{ 
	Spawn: 
		WICK B 8 A_Look 
		Loop 
	See:
		WICK AABBCCDD 3 A_Chase
		WICK A 0 A_Jump(96, "Dash")
		Goto See
	Dash:
		WICK EEFFGGHH 2 A_Chase
		TNT1 A 0 A_Jump(32, "Dash")
		Goto See
	Missile:
		TNT1 A 0 A_Stop
		WICK I 1 A_PlaySound("monster/wikatk")
		WICK I 4 A_FaceTarget
		WICK J 5 BRIGHT A_FaceTarget
		TNT1 A 0 A_CustomMissile("WickedShot1", 60, 0, 16)
		TNT1 A 0 A_CustomMissile("WickedShot2", 60, 0, -16)
		WICK K 7 BRIGHT A_CustomMissile("WickedShot3",60,0,0)
		WICK L 38
		Goto See       
	Pain: 
		WICK M 3 
		WICK M 3 A_Pain 
		WICK M 3 
		Goto See 
	Death.Rocket:
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_SpawnItemEx("GenericXDeathSpawner2", 0, 0, 0, 0, 0, 0,0, 129)
		WICK N 2 A_Scream
		WICK O 2
		WICK P 3
		TNT1 A 0 A_SpawnItemEX("WickedTorso",0,0,40, -10, 0, frandom(2, 6),0,16)
		Goto AirLoop
	
	Death:
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_SpawnItemEx("GenericXDeathSpawner2", 0, 0, 0, 0, 0, 0,0, 129)
		WICK N 2 A_Scream
		WICK O 2
		WICK P 3
		TNT1 A 0 A_SpawnItemEX("WickedTorso",0,0,40, -10, 0, frandom(0, 4),0,16)
	AirLoop:
		WICT A 2 A_CheckFloor("FinishDeath")
		Loop
	FinishDeath:
		TNT1 A 0 A_Stop
		TNT1 AAAAA 0 A_SpawnItemEx("NewFlyingBlood",0,-24,16,Random(5,-5),Random(5,-5),Random(3,6),0,129)
		TNT1 A 0 A_PlaySound("misc/crunch", CHAN_AUTO)
		WICT BCDEF 4 
		WICT G -1 A_SetFloorClip 
		Stop 
	Raise:
		WICT G 0 A_RemoveChildren(TRUE)
		WICK G 0 A_SpawnItemEx("WickedTorsoRes",0,0,0,0,0,1,0,0)
		WICT G 5 A_UnSetFloorClip
		WICT FEDBC 5
		WICK PON 5
		Goto See
	} 
}

// Only fires a single, slower shot
Actor TangoWickedSlow : TangoWicked
{
	States
	{
	Missile:
		TNT1 A 0 A_Stop
		WICK I 1 A_PlaySound("monster/wikatk")
		WICK I 4 A_FaceTarget
		WICK J 5 BRIGHT A_FaceTarget
		WICK K 7 BRIGHT A_CustomMissile("WickedShotSlow",60,0,0)
		WICK L 38
		Goto See
	}
}

const int wicked_shot_threshold = 7;
const int wicked_shot_max_turn_angle = 8;

Actor WickedShot1
{
	var int user_loops_remaining;

	Radius 8
	Height 8
	Speed 20
	Damage (20)
	DamageType Fire
	Projectile
	Scale 0.75
	+ThruGhost
	+SeekerMissile
	+ForceXYBillboard
	+RANDOMIZE
	SeeSound "weapons/firmfi"
	DeathSound "weapons/firex5"
	Decal "DoomImpScorch"
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_SetUserVar("user_loops_remaining", 2)
		TNT1 A 0 A_ScaleVelocity(0.1)
		WIBL ABABABABABAB 2 Bright 
		TNT1 A 0 A_SeekerMissile(90, 90, SMF_PRECISE , 256)
		TNT1 A 0 A_PlaySound("wicked/fireballseek", CHAN_AUTO)
		Goto SeekLoop
	SeekLoop:
		WIBL A 2 Bright A_SpawnItemEx("WickedFX",0,0,0,random(-1,1),random(-1,1),random(-1,1),0,128,0)
		TNT1 A 0 Bright A_SeekerMissile(wicked_shot_threshold, wicked_shot_max_turn_angle, SMF_PRECISE, 256)
		
		WIBL B 2 Bright A_SpawnItemEx("WickedFX",0,0,0,random(-1,1),random(-1,1),random(-1,1),0,128,0)
		TNT1 A 0 Bright A_SeekerMissile(wicked_shot_threshold, wicked_shot_max_turn_angle, SMF_PRECISE, 256)
		
		WIBL A 2 Bright A_SpawnItemEx("WickedFX",0,0,0,random(-1,1),random(-1,1),random(-1,1),0,128,0)
		TNT1 A 0 Bright A_SeekerMissile(wicked_shot_threshold, wicked_shot_max_turn_angle, SMF_PRECISE, 256)
		
		WIBL B 2 Bright A_SpawnItemEx("WickedFX",0,0,0,random(-1,1),random(-1,1),random(-1,1),0,128,0)
		TNT1 A 0 Bright A_SeekerMissile(wicked_shot_threshold, wicked_shot_max_turn_angle, SMF_PRECISE, 256)
		
		TNT1 A 0 A_SetUserVar("user_loops_remaining", user_loops_remaining - 1)
		TNT1 A 0 A_JumpIf(user_loops_remaining == 0, "NoSeekLoop")
		
		Loop
	NoSeekLoop:
		WIBL ABAB 2 Bright // A_SpawnItemEx("WickedFX",0,0,0,random(-1,1),random(-1,1),random(-1,1),0,128,0)
		Loop
	Death:
		WIBL JKLMN 3 Bright
		Stop
	}
}

Actor WickedShot2 : WickedShot1
{
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_SetUserVar("user_loops_remaining", 2)
		TNT1 A 0 A_ScaleVelocity(0.1)
		WIBL ABABABABABABABAB 2 Bright 
		TNT1 A 0 A_SeekerMissile(90, 90, SMF_PRECISE , 256)
		TNT1 A 0 A_PlaySound("wicked/fireballseek", CHAN_AUTO)
		Goto SeekLoop
	}
}

Actor WickedShot3 : WickedShot1
{
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_SetUserVar("user_loops_remaining", 2)
		TNT1 A 0 A_ScaleVelocity(0.1)
		WIBL ABABABABABABABABABAB 2 Bright 
		TNT1 A 0 A_SeekerMissile(90, 90, SMF_PRECISE , 256)
		TNT1 A 0 A_PlaySound("wicked/fireballseek", CHAN_AUTO)
		Goto SeekLoop
	}
}

Actor WickedShotSlow
{
	var int user_loops_remaining;

	Radius 8
	Height 8
	Speed 10
	Damage (20)
	DamageType Fire
	Projectile
	Scale 0.75
	+ThruGhost
	+SeekerMissile
	+ForceXYBillboard
	+RANDOMIZE
	SeeSound "weapons/firmfi"
	DeathSound "weapons/firex5"
	Decal "DoomImpScorch"
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_SetUserVar("user_loops_remaining", 1)
		TNT1 A 0 A_ScaleVelocity(0.2)
		WIBL ABABABABABAB 2 Bright 
		TNT1 A 0 A_SeekerMissile(90, 90, SMF_PRECISE , 256)
		TNT1 A 0 A_PlaySound("wicked/fireballseek", CHAN_AUTO)
		Goto SeekLoop
	SeekLoop:
		WIBL A 2 Bright A_SpawnItemEx("WickedFX",0,0,0,random(-1,1),random(-1,1),random(-1,1),0,128,0)
		TNT1 A 0 Bright A_SeekerMissile(wicked_shot_threshold, wicked_shot_max_turn_angle, SMF_PRECISE, 256)
		
		WIBL B 2 Bright A_SpawnItemEx("WickedFX",0,0,0,random(-1,1),random(-1,1),random(-1,1),0,128,0)
		TNT1 A 0 Bright A_SeekerMissile(wicked_shot_threshold, wicked_shot_max_turn_angle, SMF_PRECISE, 256)
		
		WIBL A 2 Bright A_SpawnItemEx("WickedFX",0,0,0,random(-1,1),random(-1,1),random(-1,1),0,128,0)
		TNT1 A 0 Bright A_SeekerMissile(wicked_shot_threshold, wicked_shot_max_turn_angle, SMF_PRECISE, 256)
		
		WIBL B 2 Bright A_SpawnItemEx("WickedFX",0,0,0,random(-1,1),random(-1,1),random(-1,1),0,128,0)
		TNT1 A 0 Bright A_SeekerMissile(wicked_shot_threshold, wicked_shot_max_turn_angle, SMF_PRECISE, 256)
		
		TNT1 A 0 A_SetUserVar("user_loops_remaining", user_loops_remaining - 1)
		TNT1 A 0 A_JumpIf(user_loops_remaining == 0, "NoSeekLoop")
		
		Loop
	NoSeekLoop:
		WIBL ABAB 2 Bright // A_SpawnItemEx("WickedFX",0,0,0,random(-1,1),random(-1,1),random(-1,1),0,128,0)
		Loop
	Death:
		WIBL JKLMN 3 Bright
		Stop
	}
}

Actor WickedFX
{   
  Radius 2
  Height 2
  Scale 0.5
  +ForceXYBillboard
	+NOINTERACTION
  RenderStyle Add
  Alpha 0.67
  States
  {
  Spawn:
    TNT1 A 3 Bright
    WIBL EFGHI 3 BRIGHT
    Stop 
  }
}

Actor WickedTorso
{
  Mass 1000000
  Radius 16
  Height 16
	Decal BloodSplat
	BounceType "Doom"
	+USEBOUNCESTATE
	+MISSILE
	+NOTELEPORT
	+THRUACTORS
	+CLIENTSIDEONLY
	+NOBLOCKMAP
	BounceCount 2
	BounceFactor 0.4
  States
  {
  Spawn:
    TNT1 A 0 // Without this, the sound below will never be played
    WICK Q 5 A_PlaySound("monster/tenpn1")
	SpawnLoop:
		WICK R 5
		Loop
	Bounce:
		WICK S 5 A_PlaySound("misc/crunch", CHAN_AUTO)
		Goto SpawnLoop
  Death:
	TNT1 A 0 A_QueueCorpse
	
	TNT1 A 0 A_Stop
    TNT1 A 0 // A_SetFloorClip
    TNT1 A 0 A_PlaySound("monster/tenpn2")
    WICK STUV 3
    WICK W -1
    Stop
  }
}

Actor WickedTorsoRes
{   
  Radius 24
  Height 24
  +NoGravity
  +NoBlockMap
  +NoClip
  States
  {
  Spawn:
    WICK WVUTSRQ 5
    Stop     
  }
}
