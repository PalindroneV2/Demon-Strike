ACTOR TangoGrenadier 27713
{
	//$Category Monsters
	//$Color 12
	//$Title "TangoGrenadier"
	Tag "Grenadier"
	Health 180
	Radius 20
	Height 56
	Mass 500
	Speed 7
	PainChance 170
	PainChance "Chaingun", 100
	PainChance "FireTrap", 180
	Monster
	Species "Grenadier"
	+FLOORCLIP
	+MISSILEMORE
	+MISSILEEVENMORE
	+DONTHURTSPECIES
	SeeSound "chainguy/sight"
	PainSound "chainguy/pain"
	DeathSound "chainguy/death"
	ActiveSound "chainguy/active"
	AttackSound "chainguy/attack"
	Obituary "%o got blown up by a Grenadier."
	States
	{
	Spawn:
		GRDR A 10 A_Look
		Loop
	See:
		GRDR AABBCCDD 3 A_Chase
		Loop
	MissileCancel:
		GRDR GGG 3 A_Pain
		Goto See
	Missile:
		/* IDEAL LOGIC (not implemented yet)
		
			* if anything blocking LoF within 40px, cancel
			* if anything within ~256px, lob it up
			* maybe also do a height check where, if 32px above the monster to the player is blocked,
				then that means there's a roof or something in the nade's way on its arc to the apex,
				so withold shooting, or shoot with no pitch offset
		*/
	
	
		// Continue only if there's no non-target obstacle within 40px of grenadier
// 		TNT1 A 0 A_CheckLoF("MissileContinue", 0, 40)
// 		Goto MissileCancel
	
	MissileContinue:
		GRDR E 16 A_FaceTarget
		TNT1 A 0 A_PlaySound("grenadier/fire", CHAN_WEAPON)
		// tango_grenadier_get
		//TNT1 A 0 A_SpawnProjectile("TangoGrenade", 32, 6, 0, CMF_OFFSETPITCH , ACS_NamedExecuteWithResult("tango_grenadier_get"))
		TNT1 A 0 A_CheckRange(1024, "MissileFar")
		TNT1 A 0 A_CheckLoF("MissileNormal")
		Goto MissileHigh
		
	MissileHigh:
		TNT1 A 0 A_CustomMissile("TangoGrenade", 32, 6, 0, CMF_OFFSETPITCH, 30)
		Goto FinishMissile
		
	MissileFar:
		TNT1 A 0 A_CustomMissile("TangoGrenadeFast", 32, 6, 0, CMF_OFFSETPITCH, 20)
		Goto FinishMissile
		
	MissileNormal:
		TNT1 A 0 A_CustomMissile("TangoGrenade", 32, 6, 0, CMF_OFFSETPITCH , 15)
		Goto FinishMissile
		
	FinishMissile:
		TNT1 A 0 A_FaceTarget
		GRDR F 4 Bright
		GRDR E 8
		Goto See
	Pain:
		GRDR G 3
		GRDR G 3 A_Pain
		Goto See
	Death.Massacre:
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		GRDR N -1
		Stop
	Raise:
		GRDR N 5
		GRDR MLKJIH 5
		Goto See
		
	// ****************************
	// DAMAGE TYPE DEATH SWITCHES
	// ****************************
	Death:
	XDeath:
		Goto BackDeath
	
	Death.Rocket:
		Goto ExplodeDeath
		
	 Death.Shotgun:
		TNT1 A 0 ThrustThing(angle*256/360+128, 1)
		TNT1 A 0 ThrustThingZ(0, 4, 0, 1)
		Goto BackDeath

	// ****************************
	// DEATH ANIMATIONS
	// ****************************
	BackDeath:
		TNT1 A 0 A_Scream
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_SpawnItemEx("GenericXDeathSpawner2", 0, -1, 8, 0, 0, 0,0, 129)
		TNT1 A 0 ThrustThing(angle*256/360+128, 4)
		TNT1 A 0 ThrustThingZ(0, 20, 0, 1)
		GRD1 A 3
		GRD1 BCDEF 3
	BackDeathAirLoop:
		GRD1 D 2 A_CheckFloor("BackDeathFinish")
		Loop
	BackDeathFinish:
		TNT1 A 0 A_Stop
		TNT1 AAA 0 A_SpawnItemEx("NewFlyingBlood",0,0,16,Random(5,-5),Random(5,-5),Random(3,6),0,129)
		GRD1 E 3
		GRD1 F 3
		GRD1 G -1
		Stop
	
	ExplodeDeath:
		TNT1 A 0 A_Stop
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_PlaySound("grenade/explode", CHAN_AUTO)
		TNT1 A 0 A_Explode(84, 96, 0, false, 40)
		TNT1 A 0 A_SpawnItemEx("GenericXDeathSpawner2", 0, 0, 0, 0, 0, 0,0, 129)
		TNT1 A 0 A_SpawnItemEx("GrenadierExplosion", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERPOINTERS)
		GRD2 A 3 Bright
		GRD2 B 3 Bright A_XScream
		GRD2 CDEFGHIJ 3 Bright
		GRD2 K -1
		Stop
  }
}

Actor GrenadierExplosion
{
	Height 16
	Radius 8
	Damagetype "Rocket"
	+NOINTERACTION
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("diabolist/plumeexplode")
		GPLM ABCDEFG 2 Bright A_Explode(28, 40, XF_NOTMISSILE)
		GPLM HIJ 2 Bright
		Stop
	}
}

Actor TangoGrenade
{
	Height 16
	Radius 8
	Speed 20
	Mass 9
	Damage (10)
	Gravity 0.6
	ReactionTime 60
	BounceType "Doom"
	BounceFactor 0.6
	DamageType "Rocket"
	Projectile
	-NOGRAVITY
	+EXTREMEDEATH
	+CANBOUNCEWATER
	RenderStyle normal
	SeeSound "grenade/bounce"
	BounceSound "grenade/bounce"
	Obituary "%o was exploded by %k's grenade."
	States
	{
	Spawn:
		GREN ABCDEF 2
		Loop
	Death:
		TNT1 A 0 A_SpawnItemEx("TangoGrenadeStill", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERPOINTERS)
		Stop
	XDeath:
		TNT1 A 0 A_NoGravity
		TNT1 A 0 A_PlaySound("grenade/explode", CHAN_AUTO)
		TNT1 A 0 Radius_Quake(1, 8, 0, 1, 0)
		TNT1 A 0 A_Explode(50, 96, 0, false, 40)
		TNT1 AAAA 0 A_SpawnItemEx("TangoWeaponRocketSpark", 0, 0, 0, frandom(-6.0, 6.0), frandom(-6.0, 6.0), frandom(1.0, 5.0), 0)
		GREX A 1 Bright
		TNT1 AAAA 0 A_SpawnItemEx("TangoWeaponRocketSpark", 0, 0, 0, frandom(-6.0, 6.0), frandom(-6.0, 6.0), frandom(1.0, 5.0), 0)
		GREX A 1 Bright
		TNT1 AAAA 0 A_SpawnItemEx("TangoWeaponRocketSpark", 0, 0, 0, frandom(-6.0, 6.0), frandom(-6.0, 6.0), frandom(1.0, 5.0), 0)
		GREX A 1 Bright
		GREX BCDEF 2 Bright
		Stop
	}
}

Actor TangoGrenadeFast : TangoGrenade
{
	Speed 25
}

Actor TangoGrenadeStill
{
	Height 16
	Radius 8
	Mass 9
	ReactionTime 20
	Gravity 0.6
	DamageType "Rocket"
	Scale 0.8
	DeathSound "grenade/explode"

	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("grenade/bounce", CHAN_AUTO)
	SpawnLoop:
		GREN G 2 A_Countdown
		Loop
	Death:
		TNT1 A 0 A_PlaySound("grenade/explode", CHAN_AUTO)
		TNT1 A 0 Radius_Quake(1, 8, 0, 1, 0)
		TNT1 A 0 A_Explode(50, 96, 0, false, 40)
		TNT1 AAAA 0 A_SpawnItemEx("TangoWeaponRocketSpark", 0, 0, 0, frandom(-6.0, 6.0), frandom(-6.0, 6.0), frandom(1.0, 5.0), 0)
		GREX A 1 Bright
		TNT1 AAAA 0 A_SpawnItemEx("TangoWeaponRocketSpark", 0, 0, 0, frandom(-6.0, 6.0), frandom(-6.0, 6.0), frandom(1.0, 5.0), 0)
		GREX A 1 Bright
		TNT1 AAAA 0 A_SpawnItemEx("TangoWeaponRocketSpark", 0, 0, 0, frandom(-6.0, 6.0), frandom(-6.0, 6.0), frandom(1.0, 5.0), 0)
		GREX A 1 Bright
		GREX BCDEF 2 Bright
		Stop
	}
}