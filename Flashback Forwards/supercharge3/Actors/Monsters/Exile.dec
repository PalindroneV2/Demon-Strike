ACTOR TangoExile : ArchVile replaces Archvile
{
	//$Category "Monsters"
	//$Color 12
	//$Title "TangoExile"
    Tag "Exile"
	Health 800
	Mass 700
	GibHealth 90
	PainChance "Chaingun", 20
	PainChance "FireTrap", 180
	Species "Vile"
	DamageFactor "VileFire", 0.0
	Obituary "%o got caught in an Exile's inferno."
	DeathSound "exile/death"
	States
	{
	Spawn:
		VILE A 10 A_Look
		Loop
	See:
		VILE AABBCCDDEEFF 2 A_VileChase
		Loop
	Missile:	
		VILE G 0 A_VileStart
		VILE G 10 A_FaceTarget
		VILE H 8 A_VileTarget("ExileFire")
		VILE IJKLMN 8 A_FaceTarget
		VILE O 8 A_VileAttack
		VILE P 20
		Goto See
	Heal:
		VILE "[\]" 10
		Goto See
	Pain:
		VILE Q 10 A_Pain
		Goto See
	Death.Massacre:
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		VILE Z -1
		Stop
	
	// ****************************
	// DAMAGE TYPE DEATH SWITCHES
	// ****************************
	Death:
		Goto RegularDeath
		
	XDeath:
		Goto ExplodeDeath
	
	Death.Rocket:
		Goto ExplodeDeath
	
	Death.Shotgun:
		TNT1 A 0 A_JumpIfCloser(160, "DeathRandom1")
		Goto RegularDeath
		
	DeathRandom1:
		TNT1 A 0 A_Jump(128, "ExplodeDeath")
		Goto RegularDeath
		
	// ****************************
	// DEATH ANIMATIONS
	// ****************************
	RegularDeath:
		TNT1 A 0 A_Fall
		TNT1 A 0 A_Scream
		TNT1 AA 0 A_SpawnItemEx("NewFlyingBlood",0,0,48,Random(5,-5),Random(5,-5),Random(3,6),0,129)
		TNT1 AA 0 A_SpawnItemEx("NewFlyingBlood",0,0,32,Random(5,-5),Random(5,-5),Random(3,6),0,129)
		TNT1 AA 0 A_SpawnItemEx("NewFlyingBlood",0,0,24,Random(5,-5),Random(5,-5),Random(3,6),0,129)
		TNT1 A 0 A_SpawnItemEx("GenericXDeathSpawner4", 0, 0, 0, 0, 0, 0,0, 129)
		VILE Q 5
		VILE R 5
		VILE S 5
		VILE TUV 5
		VILE W 4
		VILE XY 4
		VILE Z -1
		Stop
		
	ExplodeDeath:
		TNT1 A 0 A_PlaySound("exile/death", CHAN_BODY)
		TNT1 A 0 A_XScream
		TNT1 A 0 A_Fall
		TNT1 AA 0 A_SpawnItemEx("NewFlyingBlood",0,0,48,Random(5,-5),Random(5,-5),Random(3,6),0,129)
		TNT1 AA 0 A_SpawnItemEx("NewFlyingBlood",0,0,32,Random(5,-5),Random(5,-5),Random(3,6),0,129)
		TNT1 AA 0 A_SpawnItemEx("NewFlyingBlood",0,0,24,Random(5,-5),Random(5,-5),Random(3,6),0,129)
		TNT1 A 0 A_SpawnItemEx("GenericXDeathSpawner2")
		VILX A 4
		VILX B 3
		VILX C 3
		VILX D 3
		VILX E 3
		VILX FG 3
		VILX H -1
		Stop
	}
}

ACTOR ExileFire : ArchvileFire replaces ArchvileFire
{
	+NOBLOCKMAP
	+NOGRAVITY
	RenderStyle Add
	Alpha 0.75
	States
	{
	Spawn:
		VIRE A 2 Nodelay A_StartFire
		VIRE BAB 2 A_Fire
		VIRE C 2 A_FireCrackle
		VIRE BCBCDCDCDEDED 2 A_Fire
		VIRE E 2 A_FireCrackle
		VIRE FEFEFGHGHGH 2 A_Fire
		Stop
	}
}