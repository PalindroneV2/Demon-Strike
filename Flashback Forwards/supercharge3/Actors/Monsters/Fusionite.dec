const int fusionite_gib_angle_start = 150;
const int fusionite_gib_angle_end = 210;

const int fusion_shot_height = 18;

Actor TangoFusionite : Arachnotron replaces Arachnotron
{
	//$Category "Monsters"
	//$Color 12
	//$Title "TangoFusionite"
	Tag "Fusionite"
	Health 500
	PainChance 100
	PainChance "Chaingun", 80
	PainChance "FireTrap", 180
	Speed 12
	Radius 48
	Obituary "%o was liquefied by a Fusionite."
	PainSound "monsters/fusionitepain"
	+MISSILEMORE
	States
	{
	Spawn:
		FSPI A 10 A_Look
		Loop
	See:
		FSPI A 2 A_BabyMetal
		FSPI ABBCC 2 A_Chase
		FSPI D 2 A_BabyMetal
		FSPI DEEFF 2 A_Chase
		Loop
	Missile:
		FSPI A 12 A_FaceTarget
	MissileLoop:
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("tango_cvar_projectile_origin_fusionite") == 1, "VanillaOriginMissileLoop")
		FSPI G 2
		FSPI H 3 A_CustomMissile("FusionShot", fusion_shot_height, 0)
		FSPI G 3 A_FaceTarget
		FSPI H 3 A_CustomMissile("FusionShot", fusion_shot_height, 0)
		FSPI G 3 A_FaceTarget
		FSPI H 3 A_CustomMissile("FusionShot", fusion_shot_height, 0)
		FSPI G 3 A_FaceTarget
		FSPI H 3 A_CustomMissile("FusionShot", fusion_shot_height, 0)
		FSPI G 10 A_SpidRefire
		Goto MissileLoop
	VanillaOriginMissileLoop:
		FSPI G 2
		FSPI H 3 A_BspiAttack
		FSPI G 3 A_FaceTarget
		FSPI H 3 A_BspiAttack
		FSPI G 3 A_FaceTarget
		FSPI H 3 A_BspiAttack
		FSPI G 3 A_FaceTarget
		FSPI H 3 A_BspiAttack
		FSPI G 10 A_SpidRefire
		Goto MissileLoop
	Pain:
		FSPI I 12 A_Pain
		Goto See
	Raise:
		FSPI ONMLKJ 6
		Goto See+1
	Death.Massacre:
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		FSPI P -1 A_BossDeath
		Stop
		
	// ****************************
	// DAMAGE TYPE DEATH SWITCHES
	// ****************************
	Death:
		TNT1 A 0 A_JumpIfCloser(200, "DeathRandom1")
		Goto RegularDeath
		
	Death.Rocket:
		TNT1 A 0 A_Jump(256, "HeadExplodeDeath", "LeftLegsOffDeath")
		Goto RegularDeath
		
	Death.Shotgun:
		TNT1 A 0 A_JumpIfCloser(144, "DeathRandom1")
		Goto RegularDeath
		
	DeathRandom1:
		TNT1 A 0 A_Jump(220, "HeadExplodeDeath", "LeftLegsOffDeath")
		Goto RegularDeath
		
	// ****************************
	// DEATH ANIMATIONS
	// ****************************
	RegularDeath:
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_NoBlocking
		TNT1 AAAAAAAAAAA 0 A_SpawnItemEx("NewFlyingBlood",0,0,random(32, 52),Random(5,-5),Random(5,-5),Random(3,6),0,129)
		TNT1 A 0 A_Scream
		TNT1 A 0 A_SpawnItemEx("GenericXDeathSpawner4")
		FSPI J 6
		FSP2 K 4
		FSPI L 6
		FSPI MN 6
		FSPI O 6
		FSPI P -1 A_BossDeath
		Stop
		
		
	LeftLegsOffDeath:
		TNT1 A 0 A_Jump(128, "RightLegsOffDeath")
		
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_SpawnItemEx("GenericXDeathSpawner2")
		TNT1 AA 0 A_CustomMissile ("FusioniteLeg", 32, 0, random(fusionite_gib_angle_start, fusionite_gib_angle_end), 2, random (0, 160))
		TNT1 A 0 ThrustThing(angle*256/360+128, 6)
		TNT1 A 0 ThrustThingZ(0, 16, 0, 1)
		FUD2 A 5
		FUD2 B 6
		FUD2 CDEF 6
		FUD2 G -1 A_BossDeath
		Stop
		
		
	RightLegsOffDeath:
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_SpawnItemEx("GenericXDeathSpawner2")
		TNT1 AA 0 A_CustomMissile ("FusioniteLeg", 32, 0, random(fusionite_gib_angle_start, fusionite_gib_angle_end), 2, random (0, 160))
		TNT1 A 0 ThrustThing(angle*256/360+128, 6)
		TNT1 A 0 ThrustThingZ(0, 16, 0, 1)
		FUD3 A 5
		FUD3 B 6
		FUD3 CDEF 6
		FUD3 G -1 A_BossDeath
		Stop
		
		
	HeadExplodeDeath:
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_SpawnItemEx("GenericXDeathSpawner2")
		TNT1 A 0 A_SpawnItemEx("GenericXDeathSpawner1")
		FUD4 A 24
		FUD4 B 4
		FUD4 CD 5
		FUD4 E -1 A_BossDeath
		Stop
    }
}


// Slower walkspeed, more time in between shots
Actor TangoFusioniteSlow : TangoFusionite
{
	Speed 10
	States
	{
	Missile:
		FSPI A 12 A_FaceTarget
	MissileLoop:
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("tango_cvar_projectile_origin_fusionite") == 1, "VanillaOriginMissileLoop")
		FSPI G 2
		FSPI H 3 A_CustomMissile("FusionShot", fusion_shot_height, 0)
		FSPI G 3 A_FaceTarget
		FSPI H 3 A_CustomMissile("FusionShot", fusion_shot_height, 0)
		FSPI G 3 A_FaceTarget
		FSPI H 3 A_CustomMissile("FusionShot", fusion_shot_height, 0)
		FSPI G 3 A_FaceTarget
		FSPI H 3 A_CustomMissile("FusionShot", fusion_shot_height, 0)
		FSPI G 15 A_SpidRefire
		Goto MissileLoop
	VanillaOriginMissileLoop:
		FSPI G 2
		FSPI H 3 A_BspiAttack
		FSPI G 3 A_FaceTarget
		FSPI H 3 A_BspiAttack
		FSPI G 3 A_FaceTarget
		FSPI H 3 A_BspiAttack
		FSPI G 3 A_FaceTarget
		FSPI H 3 A_BspiAttack
		FSPI G 15 A_SpidRefire
		Goto MissileLoop
	
    }
}

ACTOR FusioniteLeg
{
    Radius 8
    Height 8
	Speed 12
	Mass 1
    +NOBLOCKMAP
	+MISSILE
    +NOTELEPORT
    +DONTSPLASH
	+DOOMBOUNCE
    +MOVEWITHSECTOR
    +CLIENTSIDEONLY
	SeeSound "misc/xdeath4"
    DeathSound "misc/xdeath3"
	BounceFactor 0.3
	BounceCount 3
    States
    {
    Spawn:
	    TNT1 A 0 A_JumpIf(waterlevel > 1, "Death")
		FUSL ABCDEFGH 2
		Loop
	Death:
		TNT1 A 0 A_QueueCorpse
	    FUSL I -1
		Stop
    }
}

ACTOR FusionShot replaces ArachnotronPlasma
{
	Radius 13
	Height 8
	Speed 22
	FastSpeed 26
	Damage (20)
	Scale 0.75  
	RENDERSTYLE ADD
	SeeSound "monsters/fusionfire" 
	DeathSound "weapons/hrballexplode"
	PROJECTILE
	States
	{
	Spawn:
		FS03 AB 1 Bright A_SpawnItemEx("FusionTrail",0,0,0,0,0,0,0,128,0)
		Loop
	Death:	
		FS03 HIJ 3 Bright
		Stop
	}
}

ACTOR FusionTrail
{
	+Missile
	+NoGravity
	+NoBlockmap
	+NoInteraction
	+CLIENTSIDEONLY
	-Solid
	Scale 0.7
	States
	{
	Spawn:
		TNT1 A 1
	SpawnLoop:
		FS03 AB 1 A_FadeOut(0.35)
		Loop
	}
}