const int pyroCriticalHealth = 250;

Actor TangoPyroDemon 27700
{
	//$Category Monsters
	//$Color 12
	//$Title "TangoPyroDemon"
	Tag "Pyro Demon"
	Health 500
	Radius 24
	Height 64
	Mass 1000
	Speed 8
	PainChance 50
	PainChance "Chaingun", 20
	PainChance "FireTrap", 180
	Monster
	Obituary "%o was burnt to a crisp by a pyro demon"
	HitObituary "%o was melted my a pyro demon"
	+FLOORCLIP
	+DONTHARMSPECIES
	+DONTHARMCLASS
	Species "Knight"
	DamageFactor "PyroFire", 0.0
	SeeSound "pyro/see"
	ActiveSound "knight/active"
	PainSound "knight/pain"
	DeathSound "knight/death"
	States
	{
	Spawn:
		PYRO A 10 A_Look
		Loop
	See:
		PYRO AA 3 A_Chase
		PYRO BB 3 A_Chase
		PYRO CC 3 A_Chase
		PYRO DD 3 A_Chase
		Loop
	Missile:
		PYRO MN 8 Bright A_FaceTarget
		PYRO O 4 Bright A_CustomMissile ("PyroDemonComet",28,0,0,0)
		PYRO P 4 Bright
		Goto See
	Pain:
		PYRO Q 2
		PYRO Q 2 A_Pain
		Goto See
	Death.Massacre:
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		PYD1 M -1
		Stop
		
	// ****************************
	// DAMAGE TYPE DEATH SWITCHES
	// ****************************
	Death:
		Goto RegularDeath
	
	Death.Shotgun:
		TNT1 A 0 A_JumpIfCloser(100, "TorsolessDeath")
		TNT1 A 0 A_JumpIfCloser(200, "DeathRandom1")
		Goto RegularDeath
		
	Death.Bullet:
	Death.Chaingun:
		Goto HeadlessDeath
		
	Death.Rocket:
		Goto TorsolessDeath	
		
	DeathRandom1:
		TNT1 A 0 A_Jump(255, "HeadlessDeath", "TorsolessDeath")
		Goto RegularDeath
	
	
	// ****************************
	// DEATH ANIMATIONS
	// ****************************
	RegularDeath:
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_Stop
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		PYD1 A 16
		PYD1 BBBBCCCCDDDDEEEE 1
		PYD1 F 0 A_PlaySound("pyro/fade1")
		PYD1 FFFF 1 A_SpawnItemEx("PyroDemonDebrisSpawner1")
		PYD1 GGGGHHHHIIIIJJJJKKLL 1 A_SpawnItemEx("PyroDemonDebrisSpawner2")
		PYD1 M -1
		Stop
	
	HeadlessDeath:
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_Stop
		TNT1 A 0 A_SpawnItemEx("PyroDemonHead", 0, 0, 52, random(-6, -10), 0, random(4, 6), 0)
		TNT1 A 0 A_SpawnItemEx("PyroDemonHorn1", 0, -14, 68, random(-1, -4), random(-1, -4), random(2, 6), 0)
		TNT1 A 0 A_SpawnItemEx("PyroDemonHorn2", 0, 14, 68, random(-1, -4), random(1, 4), random(2, 6), 0)
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		PYD2 A 16
		PYD2 BBBBCCCCDDDDEEEE 1
		PYD2 F 0 A_PlaySound("pyro/fade1")
		PYD2 FFFF 1 A_SpawnItemEx("PyroDemonDebrisSpawner1")
		PYD1 GGGGHHHHIIIIJJJJKKLL 1 A_SpawnItemEx("PyroDemonDebrisSpawner2")
		PYD1 M -1
		Stop
	
	TorsolessDeath:
		TNT1 AAAAAAAAAAAAAAAA 0 A_SpawnItemEx("PyroDemonDebris", 0, random(18, 22), random(40, 52), random(-4, 4), random(-1, 1), random(1, 2))
		TNT1 AAAAAAAAAAAAAAAA 0 A_SpawnItemEx("PyroDemonDebris", 0, random(18, 22), random(40, 52), random(-4, 4), random(-1, 1), random(1, 2))
		TNT1 AAAAAAAAAAAAAAAA 0 A_SpawnItemEx("PyroDemonDebris", 0, random(-18, -22), random(40, 52), random(-4, 4), random(-1, 1), random(1, 2))
		TNT1 AAAAAAAAAAAAAAAA 0 A_SpawnItemEx("PyroDemonDebris", 0, random(-18, -22), random(40, 52), random(-4, 4), random(-1, 1), random(1, 2))
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_Stop
		TNT1 A 0 A_SpawnItemEx("PyroDemonHead", 0, 0, 52, random(-6, -10), 0, random(4, 6), 0)
		TNT1 A 0 A_SpawnItemEx("PyroDemonTorso", 0, 0, 48, random(-2, -6), 0, random(4, 6), 0)
		TNT1 A 0 A_SpawnItemEx("PyroDemonHorn1", 0, -14, 68, random(-1, -4), random(-1, -4), random(2, 6), 0)
		TNT1 A 0 A_SpawnItemEx("PyroDemonHorn2", 0, 14, 68, random(-1, -4), random(1, 4), random(2, 6), 0)
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		PYD3 A 16
		PYD3 B 4
		PYD3 C 12
		PYD3 F 0 A_PlaySound("pyro/fade1")
		PYD3 FFFF 1 A_SpawnItemEx("PyroDemonDebrisSpawner1")
		PYD3 GGGGHHHH 1 A_SpawnItemEx("PyroDemonDebrisSpawner2")
		PYD1 IIIIJJJJKKLL 1 A_SpawnItemEx("PyroDemonDebrisSpawner2")
		PYD1 M -1
		Stop
	}
}

Actor PyroDemonDebrisSpawner1
{
	+NOBLOCKMAP
	+THRUACTORS
	+CLIENTSIDEONLY
	+NoInteraction
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx("PyroDemonDebris", 0, random(-12, 12), random(16, 64), random(-4, 4), random(-1, 1), random(1, 2))
		TNT1 A 0 A_SpawnItemEx("PyroDemonDebris", 0, random(-12, 12), random(16, 64), random(-4, 4), random(-1, 1), random(1, 2))
		TNT1 A 0 A_SpawnItemEx("PyroDemonDebris", 0, random(-12, 12), random(16, 64), random(-4, 4), random(-1, 1), random(1, 2))
		Stop
	}
}

Actor PyroDemonDebrisSpawner2
{
	+NOBLOCKMAP
	+THRUACTORS
	+CLIENTSIDEONLY
	+NoInteraction
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx("PyroDemonDebris", 0, random(-12, 12), random(0, 32), random(-4, 4), random(-1, 1), random(1, 2))
		TNT1 A 0 A_SpawnItemEx("PyroDemonDebris", 0, random(-12, 12), random(0, 32), random(-4, 4), random(-1, 1), random(1, 2))
		TNT1 A 0 A_SpawnItemEx("PyroDemonDebris", 0, random(-12, 12), random(0, 32), random(-4, 4), random(-1, 1), random(1, 2))
		Stop
	}
}

ACTOR PyroDemonHead
{
    Radius 8
    Height 16
	Speed 7
	Gravity 0.6
	Mass 4
	Scale 0.5
	+MISSILE
    +MOVEWITHSECTOR
    +CLIENTSIDEONLY
    +NOBLOCKMAP
    +NOTELEPORT
    States
    {
    Spawn:
		PHED ABCDEFGH 3
	LifelessLoop:
		PHDL ABCDEFGH 3
		Loop
    Death:
		TNT1 A 0 A_PlaySound("pyro/fade2")
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("PyroDemonDebris", random(-2, 2), random(-4, 4), random(0, 8), random(-1, -5), random(-1, 1), random(1, 2))
		Stop
    }
}

ACTOR PyroDemonTorso
{
    Radius 8
    Height 16
	Speed 7
	Gravity 0.5
	Mass 4
	Scale 0.5
	+MISSILE
    +MOVEWITHSECTOR
    +CLIENTSIDEONLY
    +NOBLOCKMAP
    +NOTELEPORT
    States
    {
    Spawn:
		PYCT ABCD 3
	LifelessLoop:
		PYCL EFGHABCD 3
		Loop
    Death:
		TNT1 A 0 A_PlaySound("pyro/fade2")
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("PyroDemonDebris", random(-2, 2), random(-10, 10), random(0, 20), random(-2, -5), random(-1, 1), random(1, 2))
		Stop
    }
}

ACTOR PyroDemonHorn1
{
    Radius 4
    Height 8
	Speed 7
	Gravity 0.6
	Mass 4
	Scale 0.5
	BounceFactor 0.4
	+DOOMBOUNCE
	+MISSILE
    +CLIENTSIDEONLY
    +NOBLOCKMAP
    +NOTELEPORT
    +MOVEWITHSECTOR
	+THRUACTORS
    States
    {
    Spawn:
		PHN1 ABCDEFGH 3
		Loop
    Death:
		TNT1 A 0 A_QueueCorpse
		PHN1 D -1
		Stop
    }
}

ACTOR PyroDemonHorn2 : PyroDemonHorn1
{
    States
    {
    Spawn:
		PHN2 HGFEDCBA 3
		Loop
    Death:
		TNT1 A 0 A_QueueCorpse
		PHN2 A -1
		Stop
    }
}

const float fadeRate = 0.1;

Actor PyroDemonDebris
{
	Radius 2
	Height 4
	VSpeed 4
	Alpha 1.0
	+NOBLOCKMAP
	+NOINTERACTION
	+NOGRAVITY
	+CLIENTSIDEONLY
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_Jump(256, "Type1", "Type2", "Type3", "Type4", "Type5", "Type6", "Type7")
	Type1:
		TNT1 A 0 A_FadeOut(fadeRate)
		TNT1 A 0 A_CStaffMissileSlither
		PDB1 A 8
		Loop
	Type2:
		TNT1 A 0 A_FadeOut(fadeRate)
		TNT1 A 0 A_CStaffMissileSlither
		PDB2 A 8
		Loop
	Type3:
		TNT1 A 0 A_FadeOut(fadeRate)
		TNT1 A 0 A_CStaffMissileSlither
		PDB3 A 8
		Loop
	Type4:
		TNT1 A 0 A_FadeOut(fadeRate)
		TNT1 A 0 A_CStaffMissileSlither
		PDB4 A 8
		Loop
	Type5:
		TNT1 A 0 A_FadeOut(fadeRate)
		TNT1 A 0 A_CStaffMissileSlither
		PDB5 A 8
		Loop
	Type6:
		TNT1 A 0 A_FadeOut(fadeRate)
		TNT1 A 0 A_CStaffMissileSlither
		PDB6 A 8
		Loop
	Type7:
		TNT1 A 0 A_FadeOut(fadeRate)
		TNT1 A 0 A_CStaffMissileSlither
		PDB7 A 8
		Loop
	}
}

const int x1 = -1;
const int x2 = -3;
const int x3 = -4;

const int y1 = 3;
const int y2 = -3;
const int y3 = 0;

const int z1 = 4;
const int z2 = 6;
const int z3 = 8;

Actor PyroDemonComet
{
  Radius 12
  Height 24
  Speed 24
  Damage (32)
  Projectile
  +RANDOMIZE
  SeeSound "pyro/comet"
  DeathSound "behemoth/cometdeath"
  Damagetype "PyroFire"
  States
  {
  Spawn:
    PCMT AAABBBCCC 1 Bright //A_SpawnItemEx("BehemothCometTrail")
    Loop
  Death:
	//TNT1 A 0 A_SpawnItemEx("PyroDemonCometChunk", 0, 0, 0, random(-1, -7), random(-3, 3), random(4, 12), 0, SXF_TRANSFERPOINTERS)
	
	TNT1 A 0 A_SpawnItemEx("PyroDemonCometChunk", 0, 0, 0, x1 + 1, y1, z1, 0, SXF_TRANSFERPOINTERS)
	TNT1 A 0 A_SpawnItemEx("PyroDemonCometChunk", 0, 0, 0, x2 + 1, y1, z2, 0, SXF_TRANSFERPOINTERS)
	TNT1 A 0 A_SpawnItemEx("PyroDemonCometChunk", 0, 0, 0, x3 + 1, y1, z3, 0, SXF_TRANSFERPOINTERS)
	
	TNT1 A 0 A_SpawnItemEx("PyroDemonCometChunk", 0, 0, 0, x1 + 1, y2, z1, 0, SXF_TRANSFERPOINTERS)
	TNT1 A 0 A_SpawnItemEx("PyroDemonCometChunk", 0, 0, 0, x2 + 1, y2, z2, 0, SXF_TRANSFERPOINTERS)
	TNT1 A 0 A_SpawnItemEx("PyroDemonCometChunk", 0, 0, 0, x3 + 1, y2, z3, 0, SXF_TRANSFERPOINTERS)
	
	TNT1 A 0 A_SpawnItemEx("PyroDemonCometChunk", 0, 0, 0, x1, y3, z1, 0, SXF_TRANSFERPOINTERS)
	TNT1 A 0 A_SpawnItemEx("PyroDemonCometChunkWithSound", 0, 0, 0, x2, y3, z2, 0, SXF_TRANSFERPOINTERS)
	TNT1 A 0 A_SpawnItemEx("PyroDemonCometChunk", 0, 0, 0, x3, y3, z3, 0, SXF_TRANSFERPOINTERS)
	
	
    PCMD ABCDEFGHIJKLMN 2 Bright
    Stop
  }
}

Actor PyroDemonCometChunk
{
	Radius 2
	Height 4
	+CORPSE
	+THRUACTORS
	+NOTELEPORT
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_Jump(256, "Type1", "Type2", "Type3")
	Type1:
		PCC1 A 2 A_SpawnItemEx("PyroDemonCometChunkTrail")
		Loop
	Type2:
		PCC2 A 2 A_SpawnItemEx("PyroDemonCometChunkTrail")
		Loop
	Type3:
		PCC3 A 2 A_SpawnItemEx("PyroDemonCometChunkTrail")
		Loop
	Crash:
		TNT1 A 0 A_SpawnItemEx("PyroDemonFloorFire", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERPOINTERS)
		Stop
	Death:
		Stop
	}
}

Actor PyroDemonCometChunkWithSound : PyroDemonCometChunk
{
	States
	{
	Crash:
		TNT1 A 0 A_SpawnItemEx("PyroDemonFloorFireWithSound", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERPOINTERS)
		Stop
	}
}

Actor PyroDemonCometChunkTrail
{
	Radius 1
	Height 1
	Speed 0
	Alpha 0.6
	Scale 0.25
	+NoBlockMap
	+NoGravity
	+NoInteraction
	+ClientSideOnly
	States
	{
	Spawn:
		PCCT ABCDEFGHIJ 1 Bright
		Stop
	}
}

Actor PyroDemonFloorFire
{
	+NoClip
	+Missile
	+DropOff
	+Randomize
	+MTHRUSPECIES
	+DONTSPLASH
	+NOTELEPORT
	Scale 1
	Speed 0
	RenderStyle Add
	Alpha 0.9
	Damagetype "PyroFire"
	States
	{
	Spawn:
	FireStart:
		PYFF ABCDEFGDEFGDEFGDEFGHIJKLMNO 3 Bright A_Explode(5, 13)
		Goto Death
	Death:
		TNT1 A 0
		Stop
	}
}

Actor PyroDemonFloorFireWithSound : PyroDemonFloorFire
{
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_PlaySound("pyro/groundfire", CHAN_6, 1, TRUE)
		Goto FireStart
	Death:
		TNT1 A 0 A_StopSound(CHAN_6)
		Stop
	}
}