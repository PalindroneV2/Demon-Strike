ACTOR TangoTorturedSoul 27701
{ 
	//$Category "Monsters"
	//$Color 12
	//$Title "TangoTorturedSoul"
	Tag "Tortured Soul"
	Health 360
	Speed 8
	// FastSpeed 10
	Radius 31
	Height 56 
	PainChance 64
	PainChance "Chaingun", 20
	PainChance "FireTrap", 180
	Mass 900 
	REACTIONTIME 8 
	MONSTER 
	+NOGRAVITY 
	+FLOAT
	+DONTHARMSPECIES
	+DONTHARMCLASS
	DamageFactor "Poison", 0
	SeeSound "torturedsoul/sight" 
	PainSound "torturedsoul/pain" 
	DeathSound "torturedsoul/death" 
	ActiveSound "torturedsoul/active" 
	Obituary "%o was tormented by the Tortured Soul." 
	States 
	{ 
	Spawn: 
		TORT A 10 A_Look 
		Loop 
	See: 
		TORT AABBCC 3 A_Chase 
		Loop 
	Missile: 
		TORT D 10 A_FaceTarget 
		TORT E 4 A_FaceTarget 
		TORT E 0 A_PlaySound("torturedsoul/attack") 
		TORT F 4 A_CustomMissile("TorturedSoulBall", 40, 0, 0) 
		Goto See 
	Pain:
		TORT G 1 A_PlaySound("torturedsoul/poison")
		// We need the spawning of the projectile to not be the first pain state,
		// otherwise it seems to trigger for every bullet that causes pain. So if you
		// shoot it with a shotgun, it shoots more poison than intended
		TNT1 AAAA 0 A_CustomMissile("TorturedSoulPoison", 32, 0, random(0, 315))
		TORT G 2
		TORT G 3 A_Pain 
		Goto See 
	Death.Massacre:
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_Scream
		Stop
	  
	// ****************************
	// DEATH ANIMATIONS
	// ****************************
	Death:
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_Scream
		TORT H 3
		TORT I 3
		TORT JK 3 
		TORT L 0 A_CustomMissile("TorturedSoulPoison", 32, 0, 0) 
		TORT L 0 A_CustomMissile("TorturedSoulPoison", 32, 0, 45) 
		TORT L 0 A_CustomMissile("TorturedSoulPoison", 32, 0, 90) 
		TORT L 0 A_CustomMissile("TorturedSoulPoison", 32, 0, 135) 
		TORT L 0 A_CustomMissile("TorturedSoulPoison", 32, 0, 180) 
		TORT L 0 A_CustomMissile("TorturedSoulPoison", 32, 0, 225) 
		TORT L 0 A_CustomMissile("TorturedSoulPoison", 32, 0, 270) 
		TORT L 0 A_CustomMissile("TorturedSoulPoison", 32, 0, 315)
		TNT1 A 0 A_SpawnItemEx("GenericXDeathSpawner4",0,0,0, 0, 0, 0,0,129)
		TNT1 AAAA 0 A_CustomMissile("TorturedSoulChunk1", 30, 0, random (0, 360), 2, random(10, 60))
		TNT1 AA 0 A_CustomMissile("TorturedSoulChunk2", 36, 0, random (0, 360), 2, random(10, 60))
		TORT L 4
		TORT M 4
		TORT N 4 
		Stop 
	} 
}

actor TorturedSoulBall 
{
    Speed 10
	// FastSpeed 12
	Radius 8
	Height 8
	Damage (20)
	Scale 0.75
    PROJECTILE 
    RENDERSTYLE ADD 
    ALPHA 0.67 
    DeathSound "imp/shotx" 
    States 
    { 
    Spawn:
		TNT1 A 0 Nodelay A_CStaffMissileSlither
		HEFG A 1 A_CustomMissile("TorturedSoulBallTrail", random(-2, 2), random(-2, 2))
		TNT1 A 0 A_BishopMissileWeave
		HEFG A 1
		TNT1 A 0 A_CStaffMissileSlither
		HEFG B 2 A_CustomMissile("TorturedSoulBallTrail", random(-2, 2), random(-2, 2))
		TNT1 A 0 A_BishopMissileWeave
		HEFG B 1
        Loop 
    Death: 
		HEFG CD 3
		HEFG EF 2
		HEFG GH 2
        Stop 
    } 
}

Actor TorturedSoulBallTrail
{
	Radius 1
	Height 1
	Speed 0
	Alpha 0.7
	Scale 0.32
	+NOGRAVITY
	+NOINTERACTION
	+CLIENTSIDEONLY
	States
	{
	Spawn:
		TNT1 A 2
	SpawnLoop:
		GLIT DEFGHIJKLMNO 1 A_FadeOut(0.025)
		Stop
	}
}

actor TorturedSoulPoison 
{
    Radius 2
    Height 2
    Speed 2
    PROJECTILE
	+THRUACTORS
    RENDERSTYLE Translucent
	DamageType "Poison"
	Scale 0.75
    Alpha 0.5
    States 
    { 
    Spawn: 
        TRPS AABBCCDDEEFFGGDDEEFFGGDDEEFFGGDDEEFFGGDDEEFFGGDDCCBBAA 2 A_Explode(2, 24, 0, false, 24)
        Stop 
    Death: 
        TRPS DDCCBBAA 2 A_Explode(2, 24, 0, false, 24)
        Stop 
    } 
}

ACTOR TorturedSoulChunk1
{
    Radius 2
    Height 2
	Speed 7
	Gravity 0.3
	Mass 4
	BounceFactor 0.4
	wallbouncefactor 0.2
	SeeSound "misc/xdeath4"
    DeathSound "misc/xdeath4"
	+MISSILE
    +MOVEWITHSECTOR
    +CLIENTSIDEONLY
    +NOBLOCKMAP
    +NOTELEPORT
    States
    {
    Spawn:
		TSGI ABCDEFGH 2 A_SpawnItemEx("NewFlyingBlood",0,0,8,Random(5,-5),Random(5,-5),Random(3,6),0)
		Loop
    Death:
		TSGI Q 0 A_QueueCorpse
        TSGI Q -1
        Stop
    }
}

ACTOR TorturedSoulChunk2
{
    Radius 2
    Height 2
	Speed 7
	Gravity 0.3
	Mass 4
	BounceFactor 0.4
	wallbouncefactor 0.2
	SeeSound "misc/xdeath4"
    DeathSound "misc/xdeath4"
	+MISSILE
    +MOVEWITHSECTOR
    +CLIENTSIDEONLY
    +NOBLOCKMAP
    +NOTELEPORT
    States
    {
    Spawn:
		TSGI IJKLMNOP 2 A_SpawnItemEx("NewFlyingBlood",0,0,8,Random(5,-5),Random(5,-5),Random(3,6),0)
		Loop
    Death:
		TSGI R 0 A_QueueCorpse
        TSGI R -1
        Stop
    }
}
