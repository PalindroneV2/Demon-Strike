ACTOR TangoShotgunGuy replaces ShotgunGuy
{
  //$Category Monsters
  //$Color 12
  //$Title "TangoShotgunGuy"
  Tag "Shotgun Guy"
  Health 30
  Radius 20
  Height 56
  Mass 100
  Speed 8
  PainChance 170
  PainChance "Chaingun", 100
  PainChance "FireTrap", 180
  Monster
  +FLOORCLIP
  SeeSound "shotguy/sight"
  AttackSound "shotguy/attack"
  PainSound "shotguy/pain"
  DeathSound "shotguy/death"
  ActiveSound "shotguy/active"
  Obituary "$OB_SHOTGUY"
  DropItem "Shotgun"
  States
  {
	Spawn:
		SGUY A 10 A_Look
		Loop
	See:
		SGUY AABBCCDD 3 A_Chase
		Loop
	Missile:
		SGUY E 10 A_FaceTarget
		TNT1 A 0 A_PlaySound("shotguy/attack", CHAN_WEAPON)
		TNT1 A 0 A_CustomBulletAttack(22.5, 0, 3, 9, "TBulletPuff", 0, CBAF_NORANDOM)
		TNT1 A 0 A_SpawnItemEx("MonsterShotgunCasing",random(0,1),-5,random(27,29), random(1,3),0,random(4,6), random(-85,-95))
		SGUY F 10
		SGUY E 10
		Goto See
	Pain:
		SGUY G 3
		SGUY G 3 A_Pain
		Goto See
	Death.Massacre:
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		SGUY L -1
		Stop
	Raise:
		SGUY L 5
		SGUY KJIH 5
		Goto See
		
		
	// ****************************
	// DAMAGE TYPE DEATH SWITCHES
	// ****************************
	Death:
		Goto RegularDeath
		
	Death.Shotgun:
		TNT1 A 0 A_JumpIfCloser(200, "DeathRandom1")
		Goto RegularDeath
		
	DeathRandom1:
		TNT1 A 0 A_Jump(200, "FlyDeath")
		TNT1 A 0 ThrustThing(angle*256/360+128, 2)
		TNT1 A 0 ThrustThingZ(0, 6, 0, 1)
		Goto RegularDeath
	
	Death.Rocket:
		Goto ExplodeDeath
		
	XDeath:
		Goto ExplodeDeath
	
	
	// ****************************
	// DEATH ANIMATIONS
	// ****************************
	RegularDeath:
		TNT1 AAAAAAAAAA 0 A_SpawnItemEx("NewFlyingBlood",0,0,random(16, 48),Random(5,-5),Random(5,-5),Random(3,6),0,129)
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		SGUY H 5
		SGUY I 5
		SGUY J 5
		SGUY K 5 A_Stop
		TNT1 A 0 A_PlaySound("barrel/pain")
		TNT1 A 0 A_SpawnItemEx("ShotgunGuyHelmet", 0, 0, 4, -2, frandom(-1, 1), 2, 0)
		SGUY L -1
		Stop
		
	
	FlyDeath:
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 ThrustThing(angle*256/360+128, 6)
		TNT1 A 0 ThrustThingZ(0, 32, 0, 1)
		TNT1 A 0 A_SpawnItemEx("GenericXDeathSpawner4")
		TNT1 A 0 A_SpawnItemEx("ShotgunGuyHelmet", 0, 0, 40, -8, 0, 6, 0)
		TNT1 AAA 0 A_SpawnItemEx("NewFlyingBlood",0,0,48,Random(5,-5),Random(5,-5),Random(3,6),0,129)
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		SYD2 A 7
		SYD2 B 7
	FlyDeathAirLoop:
		SYD2 C 2 A_CheckFloor("FlyDeathFinish")
		Loop
	FlyDeathFinish:
		TNT1 A 0 A_Stop
		TNT1 AAA 0 A_SpawnItemEx("NewFlyingBlood",0,0,10,Random(5,-5),Random(5,-5),Random(3,6),0,129)
		TNT1 A 0 A_PlaySound("misc/xdeath1")
		SGUY L -1
		Stop
		
		
	ExplodeDeath:
		TNT1 A 0 A_Stop
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_XScream
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_SpawnItemEx("ShotgunGuyHelmet", 0, 0, 40, -8, 0, 6, 0)
		TNT1 A 0 A_SpawnItemEx("GenericXDeathSpawner2", 0, 0, 0, 0, 0, 0,0, 129)
		SGUY M 4
		SGUY N 4
		SGUY O 4
		SGUY PQRS 4
		SGUY T 4 A_Stop
		SGUY U -1
		Stop

  }
}

ACTOR ShotgunGuyHelmet
{
    Radius 8
    Height 16
	Speed 7
	Gravity 0.7
	Mass 8
	BounceFactor 0.5
	WallBounceFactor 0.4
	BounceType Doom
	Scale 0.5
	BounceSound "barrel/pain"
	+MISSILE
    +MOVEWITHSECTOR
    +CLIENTSIDEONLY
    +NOBLOCKMAP
    +NOTELEPORT
	+THRUACTORS
    States
    {
    Spawn:
		SHMT ABCDEFGH 3
		Loop
    Death:
		TNT1 A 0 A_QueueCorpse
		TNT1 A 0 A_PlaySound("barrel/pain")
		SHMT A 0 A_Jump(64, "DeathB")
		SHMT A 0 A_Jump(64, "DeathC")
		SHMT A 0 A_Jump(64, "DeathD")
	DeathA:
		SHMT C -1
        Stop
	DeathB:
		SHMT B -1
        Stop
	DeathC:
		SHMT F -1
        Stop
	DeathD:
        SHMT H -1
        Stop
    }
}

// ****************************
// GOTHIC VARIANT
// ****************************
ACTOR TangoShotgunGuyGoth : TangoShotgunGuy 27714
{
  //$Category Monsters
  //$Color 12
  //$Title "TangoShotgunGuyGoth"
  Tag "Shotgun Guy"
  States
  {
	Spawn:
		GSGY A 10 A_Look
		Loop
	See:
		GSGY AABBCCDD 3 A_Chase
		Loop
	Missile:
		GSGY E 10 A_FaceTarget
		TNT1 A 0 A_PlaySound("shotguy/attack", CHAN_WEAPON)
		TNT1 A 0 A_CustomBulletAttack(22.5, 0, 3, 9, "TBulletPuff", 0, CBAF_NORANDOM)
		TNT1 A 0 A_SpawnItemEx("MonsterShotgunCasing",random(0,1),-5,random(27,29), random(1,3),0,random(4,6), random(-85,-95))
		GSGY F 10
		GSGY E 10
		Goto See
	Pain:
		GSGY G 3
		GSGY G 3 A_Pain
		Goto See
	Death.Massacre:
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		GSGY L -1
		Stop
	Raise:
		GSGY L 5
		GSGY KJIH 5
		Goto See
		
		
	// ****************************
	// DAMAGE TYPE DEATH SWITCHES
	// ****************************
	Death:
		Goto RegularDeath
		
	Death.Shotgun:
		TNT1 A 0 A_JumpIfCloser(200, "DeathRandom1")
		Goto RegularDeath
		
	DeathRandom1:
		TNT1 A 0 A_Jump(200, "FlyDeath")
		TNT1 A 0 ThrustThing(angle*256/360+128, 2)
		TNT1 A 0 ThrustThingZ(0, 6, 0, 1)
		Goto RegularDeath
	
	Death.Rocket:
		Goto ExplodeDeath
		
	XDeath:
		Goto ExplodeDeath
	
	
	// ****************************
	// DEATH ANIMATIONS
	// ****************************
	RegularDeath:
		TNT1 AAAAAAAAAA 0 A_SpawnItemEx("NewFlyingBlood",0,0,random(16, 48),Random(5,-5),Random(5,-5),Random(3,6),0,129)
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		GSGY H 5
		GSGY I 5
		GSGY J 5
		GSGY K 5 A_Stop
		GSGY L -1
		Stop
		
	
	FlyDeath:
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 ThrustThing(angle*256/360+128, 6)
		TNT1 A 0 ThrustThingZ(0, 32, 0, 1)
		TNT1 A 0 A_SpawnItemEx("GenericXDeathSpawner4")
		TNT1 A 0 A_SpawnItemEx("ShotgunGuyGothHead", 0, 0, 48, frandom(-1.0, -8.0), frandom(-3.0, 3.0), frandom(3.0, 9.0), 0)
		TNT1 AAA 0 A_SpawnItemEx("NewFlyingBlood",0,0,48,Random(5,-5),Random(5,-5),Random(3,6),0,129)
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		GSD2 A 7
		GSD2 B 7
	FlyDeathAirLoop:
		GSD2 C 2 A_CheckFloor("FlyDeathFinish")
		Loop
	FlyDeathFinish:
		TNT1 A 0 A_Stop
		TNT1 AAA 0 A_SpawnItemEx("NewFlyingBlood",0,0,10,Random(5,-5),Random(5,-5),Random(3,6),0,129)
		TNT1 A 0 A_PlaySound("misc/xdeath1")
		GSGY L -1
		Stop
		
		
	ExplodeDeath:
		TNT1 A 0 A_Stop
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_XScream
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_SpawnItemEx("ShotgunGuyGothHead", 0, 0, 48, frandom(-1.0, -8.0), frandom(-3.0, 3.0), frandom(3.0, 9.0), 0)
		TNT1 A 0 A_SpawnItemEx("GenericXDeathSpawner2", 0, 0, 0, 0, 0, 0,0, 129)
		GSGY M 4
		GSGY N 4
		GSGY O 4
		GSGY PQRS 4
		GSGY T 4 A_Stop
		GSGY U -1
		Stop

  }
}

ACTOR ShotgunGuyGothHead
{
    Radius 8
    Height 16
	Speed 7
	Gravity 0.7
	Mass 8
	BounceFactor 0.4
	BounceCount 2
	BounceType Doom
	BounceSound "misc/xdeath2"
	Decal BloodSplat
	+MISSILE
    +MOVEWITHSECTOR
    +CLIENTSIDEONLY
    +NOBLOCKMAP
    +NOTELEPORT
	+THRUACTORS
    States
    {
    Spawn:
		GSGH ABCDEFGH 3 A_SpawnItemEx("NewFlyingBlood", 0, 0, -8, 0, 0, 0, 0, 129)
		Loop
    Death:
		TNT1 A 0 A_QueueCorpse
		TNT1 A 0 A_PlaySound("misc/xdeath2")
		GSGH I -1
        Stop
    }
}