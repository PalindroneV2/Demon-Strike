ACTOR TangoSpiderMastermind replaces SpiderMastermind
{
	//$Category Monsters
	//$Color 12
	//$Title "TangoSpiderMastermind"
	Tag "Spider Mastermind"
	Health 3000
	Radius 100
	Height 100
	Mass 5000
	Speed 12
	PainChance 15
	PainChance "Chaingun", 20
	PainChance "FireTrap", 180
	Monster
	MinMissileChance 160
	+BOSS
	+MISSILEMORE
	+FLOORCLIP
	+DONTMORPH
	+BOSSDEATH
	+NORADIUSDMG
	+DOHARMSPECIES
	SeeSound "spider/sight"
	PainSound "spider/pain"
	DeathSound "spider/death"
	ActiveSound "spider/active"
	Obituary "$OB_SPIDER"
	States
	{
	Spawn:
		SPID A 10 A_Look
		Loop
	See:
		SPID A 3 A_Metal
		SPID ABB 3 A_Chase
		SPID C 3 A_Metal
		SPID CDD 3 A_Chase
		SPID E 3 A_Metal
		SPID EFF 3 A_Chase
		Loop
	Missile:
		SPID A 20 A_FaceTarget
		TNT1 A 0 A_PlaySound("spidermastermind/scrapattack", CHAN_VOICE)
		SPID G 2 A_CustomMissile("SpiderScrap", 35, 0, random(-13, 13))
		SPID G 2 A_CustomMissile("SpiderScrap", 42, 0, random(-2, 2))
		SPID H 2
		SPID H 1 A_SpidRefire
		Goto Missile+1
	Pain:
		SPID I 3
		SPID I 3 A_Pain
		Goto See
	Death.Massacre:
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_BossDeath
		SPID S -1
		Stop
	Death:
		TNT1 A 0 A_NoBlocking
		SPID J 10 A_Scream
		SPID K 7
		SPID LMN 7
		TNT1 A 0 A_SpawnItemEx("GenericXDeathSpawner1", frandom(-2.0, 2.0), frandom(-20.0, 20.0), 0, 0, 0, 0, 180, 129)
		TNT1 A 0 A_SpawnItemEx("GenericXDeathSpawner3", frandom(-2.0, 2.0), frandom(-20.0, 20.0), 0, 0, 0, 0, 0, 129)
		SPID OPQR 7
		SPID S -1 A_BossDeath
		Stop
  }
}

Actor TangoSpiderMastermindDropScrapGunCache : TangoSpiderMastermind
{
	DropItem "TangoScrapGunCacheSpawner"
}

ACTOR SpiderScrap
{
	Projectile
	Damage (13)
	Decal "ScrapChip"
	Speed 55
	Height 8
	Radius 4
	+BLOODSPLATTER
	Scale 0.75
	DamageType Shotgun
	States
	{
	Spawn:
		SSCR A 1 Bright A_SpawnItemEx("SpiderScrapTrail", 0, 0, 0, 0, 0, 0)
		Loop
	Death:
		TNT1 A 0 A_PlaySound("spidermastermind/scrapwall")
		TNT1 A 0 A_SpawnItemEx("TBulletPuff", 0, 0, 0, 0, 0, 0)
		Stop
	XDeath:
		TNT1 A 0 A_PlaySound("spidermastermind/scraphit")
		Stop
	}
}

Actor SpiderScrapTrail
{
	Radius 1
	Height 1
	Speed 0
	Alpha 0.6
	Scale 0.12
	+NOINTERACTION
	+CLIENTSIDEONLY
	+RANDOMIZE
    States
    {
    Spawn:
		TNT1 A 2
		BRBA MOPQSUVX 1 Bright
		Stop
    }
}