Actor TangoMachinegunGuy // 27703
{
	//$Category Monsters
	//$Color 12
	//$Title "TangoMachinegunGuy"
	Tag "Machinegun Guy"
	Health 70
	Radius 20
	Height 56
	Speed 10
	PainChance 150
	PainChance "Chaingun", 20
	PainChance "FireTrap", 180
	Monster
	+FLOORCLIP
	SeeSound "grunt/sight"
	PainSound "grunt/pain"
	DeathSound "grunt/death"
	ActiveSound "grunt/active"
	Obituary "%o was riddled by a machinegun guy."
	Decal "bulletchip"
	Dropitem "Chaingun"
	//DropItem "Machinegun"
	States
	{
	Spawn:
		MGPO A 10 A_Look
		Loop
	See:
		MGPO AABBCCDD 4 A_Chase
		Loop
	Missile:
		MGPO E 8 A_FaceTarget
		TNT1 A 0 A_SpawnItemEx("MonsterChaingunCasing",random(14,15),-15,random(32,33), random(1,2),0,random(4,6), random(-85,-95))
		TNT1 A 0 A_PlaySound("machinegunguy/shoot")
		TNT1 A 0 A_CustomBulletAttack(12, 0, 1, 9, "TBulletPuff", 0, CBAF_NORANDOM)
		MGPO F 2
		MGPO E 3
		
		TNT1 A 0 A_SpawnItemEx("MonsterChaingunCasing",random(14,15),-15,random(32,33), random(1,2),0,random(4,6), random(-85,-95))
		TNT1 A 0 A_PlaySound("machinegunguy/shoot")
		TNT1 A 0 A_CustomBulletAttack(12, 0, 1, 9, "TBulletPuff", 0, CBAF_NORANDOM)
		MGPO F 2
		MGPO E 2
		
		TNT1 A 0 A_SpawnItemEx("MonsterChaingunCasing",random(14,15),-15,random(32,33), random(1,2),0,random(4,6), random(-85,-95))
		TNT1 A 0 A_PlaySound("machinegunguy/shoot")
		TNT1 A 0 A_CustomBulletAttack(12, 0, 1, 9, "TBulletPuff", 0, CBAF_NORANDOM)
		MGPO F 2
		MGPO E 18 A_CPosRefire
		Goto Missile+1
	Pain:
		MGPO G 3
		MGPO G 3 A_Pain
		Goto See
	Death.Massacre:
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		MGPO L -1
		Stop
	
	// ****************************
	// DAMAGE TYPE DEATH SWITCHES
	// ****************************
	Death:
		Goto RegularDeath
		
	Death.Shotgun:
		TNT1 A 0 A_JumpIfCloser(200, "DeathRandom1")
		Goto RegularDeath
		
	DeathRandom1:
		TNT1 A 0 A_Jump(200, "LegsDeath")
		TNT1 A 0 ThrustThing(angle*256/360+128, 2)
		TNT1 A 0 ThrustThingZ(0, 6, 0, 1)
		Goto RegularDeath
		
	Death.Rocket:
		TNT1 A 0 A_Jump(160, "LegsDeath")
		Goto ExplodeDeath
		TNT1 A 0
		
	XDeath:
		Goto ExplodeDeath
		
	Death.Saw:
		Goto HalfDeath
		
	// ****************************
	// DEATH ANIMATIONS
	// ****************************
	RegularDeath:
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_Scream
		TNT1 A 0 A_FaceTarget
		TNT1 AAA 0 A_SpawnItemEx("NewFlyingBlood",0, random(-16, 16),48,Random(5,-5),Random(5,-5),Random(3,6),0,129)
		MGPO H 5
		MGPO I 5
		MGPO J 5
		MGPO K 5
		MGPO L -1
		Stop
	
	ExplodeDeath:
		TNT1 A 0 A_Stop
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_XScream
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_SpawnItemEx("GenericXDeathSpawner2", 0, 0, 0, 0, 0, 0,0, 129)
		MGD2 A 4
		MGD2 B 4
		MGD2 C 4
		MGD2 DEFGH 4
		MGD2 I -1
		Stop
		
	LegsDeath:
		TNT1 A 0 ThrustThing(angle*256/360+128, 6)
		TNT1 A 0 ThrustThingZ(0, 32, 0, 1)
		TNT1 A 0 A_SpawnItemEx("GenericXDeathSpawner1")
		TNT1 AAA 0 A_SpawnItemEx("NewFlyingBlood",0, random(-16, 16),48,Random(5,-5),Random(5,-5),Random(3,6),0,129)
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		MGD3 A 3
	LegsDeathAirLoop:
		MGD3 B 2 A_CheckFloor("LegsDeathFinish")
		Loop
	LegsDeathFinish:
		TNT1 A 0 A_Stop
		TNT1 AAA 0 A_SpawnItemEx("NewFlyingBlood",-6,random(-8, 8),10,Random(5,-5),Random(5,-5),Random(3,6),0,129)
		TNT1 A 0 A_PlaySound("misc/xdeath1")
		MGD3 C 3
		MGD3 D 3
		TNT1 AAA 0 A_SpawnItemEx("NewFlyingBlood",-6,random(-8, 8),10,Random(5,-5),Random(5,-5),Random(3,6),0,129)
		MGD3 E 5
		MGD3 F -1
		Stop
		
	HalfDeath:
		TNT1 AAA 0 A_SpawnItemEx("NewFlyingBlood",0,random(-8, 8),random(32, 48),Random(5,-5),Random(5,-5),Random(3,6),0,129)
		TNT1 A 0 A_XScream
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_FaceTarget
		MGD4 A 3
		MGD4 B 3
		MGD3 A 3 A_CustomMissile("MachinegunGuyTorso", 40, 0, random(175, 185), 2, random(10, 20))
		MGD3 BCDE 3
		MGD3 F -1
		Stop
	Raise:
		MGPO K 5
		MGPO JIH 5
		Goto See
	}
}

// Just an unused decor object
ACTOR DeadMachineGunGuy
{
  States
  {
  Spawn:
    MGPB A -1
    Stop
  }
}

ACTOR MachinegunGuyTorso
{
    Radius 2
    Height 2
	Speed 3
	Gravity 0.7
	Mass 4
	+MISSILE
    +MOVEWITHSECTOR
    +CLIENTSIDEONLY
    +NOBLOCKMAP
    +NOTELEPORT
	+THRUACTORS
    States
    {
    Spawn:
		MGTS A 3
	SpawnLoop:
		MGTS B 3 A_SpawnItemEx("NewFlyingBlood",0,0,8,Random(5,-5),Random(5,-5),Random(3,6),0, SXF_USEBLOODCOLOR)
		Loop
    Death:
		TNT1 A 0 A_QueueCorpse
		TNT1 A 0 A_PlaySound("misc/xdeath1")
		MGTS C 3
		MGTS D 5
		MGTS E 3
        MGTS F -1
        Stop
    }
}