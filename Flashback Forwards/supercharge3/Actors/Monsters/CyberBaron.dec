Actor TangoCyberBaron : BaronOfHell 27708
{
	//$Category Monsters
	//$Color 12
	//$Title "TangoCyberBaron"
	var int user_is_disarmed;

	Tag "Cyber Baron"
	Health 700
	PainChance 40
	PainChance "Bullet", 0
	PainChance "Chaingun", 0
	PainChance "Rocket", 200
	PainChance "Saw", 200
	PainChance "FireTrap", 180
	MeleeDamage 8
	Obituary "%o was annihilated by a Cyber Baron."
	HitObituary "%o got whacked by a Cyber Baron."
	SeeSound "cyberbaron/see"
	PainSound "cyberbaron/pain"
	DeathSound "cyberbaron/death"
	ActiveSound "cyberbaron/active"
	+BOSSDEATH
	+DONTHARMSPECIES
	+DONTHARMCLASS
	Species "Knight"

	States
	{
	Spawn:
		CBAR A 0 NoDelay A_SetUserVar("user_is_disarmed", 0)
	SpawnLoop:
		CBAR A 7 A_Look
		Loop
	See:
		TNT1 A 0 A_JumpIf(user_is_disarmed, "SeeDisarmed")
		CBAR AABBCCDD 3 A_Chase
		Loop
	SeeDisarmed:
		CBAD AABBCCDD 3 A_Chase
		Loop
	Missile:
		TNT1 A 0 A_JumpIf(user_is_disarmed, "BaronBallDisarmed")
		TNT1 A 0 A_Jump(160, "Rocket")
		Goto BaronBall
	Rocket:
		TNT1 A 0 A_PlaySound("cyberbaron/see")
		CBAR E 14 A_FaceTarget
		CBAR F 8 A_CustomMissile("Rocket",32,16,0,0)
		CBAR E 10
		Goto See
	BaronBall:
		CBAR PQ 7 A_FaceTarget
		CBAR R 5 A_CustomMissile("BaronBall", 32, 0, 0)
		Goto See
	BaronBallDisarmed:
		CBAD PQ 7 A_FaceTarget
		CBAD R 5 A_CustomMissile("BaronBall", 32, 0, 0)
		Goto See
	Melee:
		TNT1 A 0 A_JumpIf(user_is_disarmed, "MeleeDisarmed")
		CBAR PQ 7 Fast A_FaceTarget
		CBAR R 7 Fast A_CustomMeleeAttack(40, "baron/melee")
		Goto See
	MeleeDisarmed:
		CBAD PQ 7 Fast A_FaceTarget
		CBAD R 7 Fast A_CustomMeleeAttack(40, "baron/melee")
		Goto See
	Raise:
		TNT1 A 0 A_JumpIf(user_is_disarmed, "RaiseDisarmed")
		Goto RaiseRegular
	RaiseDisarmed:
		CBD1 JIHGFED 3
		CBD2 CBA 3
		Goto See
	RaiseRegular:
		CBD1 JIHGFEDCBA 3
		Goto See
	Pain.Saw:
		TNT1 A 0 A_JumpIf(user_is_disarmed, "PainDisarmed")
		Goto PainBecomeDisarmed
	Pain:
		TNT1 A 0 A_JumpIf(user_is_disarmed, "PainDisarmed")
		TNT1 A 0 A_JumpIf(health <= 400, "PainBecomeDisarmed")
		CBAR H 4 A_Pain
		Goto See
	PainBecomeDisarmed:
		// TNT1 A 0 A_SetPainThreshold(1000) // doesn't work in Zandronum
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_SetUserVar("user_is_disarmed", true)
		TNT1 A 0 A_CustomMissile("TangoCyberBaronRocketArm", 32, 32, random (170, 220), 2, random(0, 30))
		TNT1 A 0 A_SpawnItemEx("GenericXDeathSpawner2", 0, 32, -8)
		TNT1 AA 0 A_SpawnItemEx("NewFlyingBlood",0,0,14,Random(5,-5),Random(5,-5),Random(3,6),0,129)
		TNT1 A 0 A_PlaySound("cyberbaron/disarm")
		CBAD HHHHHHH 8 A_SpawnItemEx("NewFlyingBlood",0,14,50,Random(1,-1),Random(1,3),Random(-3,-1),0,129)
		// TNT1 A 0 A_SetPainThreshold(0) // doesn't work in Zandronum
		Goto See
	PainDisarmed:
		CBAD H 4 A_Pain
		Goto See
	Death.Massacre:
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		CBD1 J -1
		Stop
	// ****************************
	// DAMAGE TYPE DEATH SWITCHES
	// ****************************
	Death:
		Goto RegularDeath
		
	// ****************************
	// DEATH ANIMATIONS
	// ****************************
	RegularDeath:
		TNT1 A 0 A_Stop
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		TNT1 AA 0 A_SpawnItemEx("TangoRocketAmmo", 0, 0, 16, frandom(1, 3), frandom(-1, 1), random(2, 4))
		TNT1 A 0 A_JumpIf(user_is_disarmed, "DeathDisarmed")
		CBD1 A 5 
		CBD1 B 4
		CBD1 C 3
		Goto DeathFinish

	DeathDisarmed:
		CBD2 A 5
		CBD2 B 4
		CBD2 C 3
		Goto DeathFinish
		
	DeathFinish:
		TNT1 A 0 A_SpawnItemEx("GenericXDeathSpawner2")
		TNT1 A 0 A_SpawnItemEx("NewFlyingBlood",0,0,14,Random(5,-5),Random(5,-5),Random(3,6),0,129)
		TNT1 A 0 A_SpawnItemEx("NewFlyingBlood",0,0,14,Random(5,-5),Random(5,-5),Random(3,6),0,129)
		CBD1 D 4
		CBD1 EFGHI 4
		CBD1 J -1 A_BossDeath
		Stop
    }
}

ACTOR TangoCyberBaronRocketArm
{
    Radius 8
    Height 16
	Speed 5
	Gravity 0.5
	Mass 4
	BounceFactor 0.7
	wallbouncefactor 0.5
	BounceType Doom
	BounceSound "cyberbaron/armbounce"
	Scale 0.5
	+MISSILE
    +MOVEWITHSECTOR
    +CLIENTSIDEONLY
    +NOBLOCKMAP
    +NOTELEPORT
	+THRUACTORS
    States
    {
    Spawn:
		CBRM ABCDEFGH 3 A_SpawnItemEx("NewFlyingBlood",0,0,8,Random(5,-5),Random(5,-5),Random(3,6),0, SXF_USEBLOODCOLOR)
		Loop
    Death:
		TNT1 A 0 A_QueueCorpse
		TNT1 A 0 A_Jump(128, "AltDeath")
		TNT1 A 0 A_PlaySound("cyberbaron/armbounce")
        CBRM I -1
        Stop
	AltDeath:
		CBRM J -1
		Stop
    }
}