Actor TangoStomper : DoomWeapon /*replaces RocketLauncher*/ 28703
{
	//$Category Weapons
	//$Title "TangoStomper"
	Tag "Stomper"
	+WEAPON.NOALERT
	+WEAPON.AMMO_OPTIONAL
	-WEAPON.NOAUTOFIRE
	Weapon.SlotNumber 5
	Weapon.SelectionOrder 50
	Weapon.AmmoType "TangoStomperAmmo"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 2
	Inventory.PickupMessage "$TANGO_STOMPER"
	Inventory.PickupSound "stomper/pickup"
	Weapon.KickBack 50
	States
	{
	Spawn:
		CRSP A -1
		Stop
	Select:
		TNT1 A 0 A_PlaySound("stomper/pickup", CHAN_WEAPON)
		CRSH A 0 A_Raise
		Loop
	Deselect:
		CRSH A 0 A_Lower
		Loop
	Ready:
		CRSH A 1 A_WeaponReady
		Loop
	NoAmmo:
		CRSH A 2 A_PlaySound("weapons/empty")
		Goto Ready
	Fire:
		TNT1 A 0 A_JumpIfInventory("TangoStomperAmmo", 1, 1)
        Goto NoAmmo
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_Light1
		TNT1 A 0 A_PlaySound("stomper/fire", CHAN_WEAPON)
		
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("tango_cvar_enable_recoil") == 0, "SkipRecoil1")
		TNT1 A 0 A_SetPitch(pitch - 1.6)
		SkipRecoil1:
		
		TNT1 A 0 A_FireCustomMissile("TangoStomperMissile1", 0, 1)
		
		CRSF A 1 Bright
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("tango_cvar_enable_screen_shake") == 0, "SkipShake")
		TNT1 A 0 Radius_Quake(3, 4, 0, 1, 0)
		SkipShake:
		
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("tango_cvar_enable_recoil") == 0, "SkipRecoil2")
		TNT1 A 0 A_SetPitch(pitch + 0.8)
		SkipRecoil2:
		
		TNT1 A 0 A_Light2
		TNT1 A 0 A_FireCustomMissile("TangoStomperMissile2", -3.25, 0, 0, 0, 0, frandom(-2.0, 2.0))
		TNT1 A 0 A_FireCustomMissile("TangoStomperMissile2", 3.25, 0, 0, 0, 0, frandom(-1.0, 1.0))
		CRSF B 1 Bright
		
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("tango_cvar_enable_recoil") == 0, "SkipRecoil3")
		TNT1 A 0 A_SetPitch(pitch + 0.8)
		SkipRecoil3:
		
		TNT1 A 0 A_FireCustomMissile("TangoStomperMissile3", -5.5, 0, 0, 0, 0, frandom(-2.0, 2.0))
		CRSF B 1 Bright
		TNT1 A 0 A_FireCustomMissile("TangoStomperMissile3", 5.5, 0, 0, 0, 0, frandom(-1.0, 1.0))
		CRSF C 2 Bright A_Light0
		CRSF D 2 Bright
		CRSF E 1
		CRSF F 2
		CRSH A 14
		CRSH A 1 A_Refire
		Goto Ready
	}
}

// player-fired rockets
Actor TangoStomperMissile1
{
	var int user_loops_remaining;

	Radius 3
	Height 6
	Projectile
	Damage (20)
	Speed 28
	DamageType "Rocket"
	+DEHEXPLOSION
	Obituary "$OB_MPROCKET" // "%o rode %k's rocket."
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_SetUserVar("user_loops_remaining", 10)
		Goto SpawnLoop
	SpawnLoop:
		TNT1 A 0 A_Weave(2, 2, frandom(-3, 3), frandom(-3, 3))
		CRMS A 2 Bright A_SpawnItemEx("TangoStomperMissileTrail", 0, 0, 0, 0, frandom(0, -0.25), frandom(-0.25, 0.25))
		TNT1 A 0 A_SetUserVar("user_loops_remaining", user_loops_remaining - 1)
		TNT1 A 0 A_JumpIf(user_loops_remaining == 0, "Death")
		Loop
	Death:
		TNT1 A 0 A_Stop
		TNT1 A 7 A_SpawnItemEx("TangoStomperMissileExplosion", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION | SXF_TRANSFERPOINTERS)
		TNT1 A 7 A_SpawnItemEx("TangoStomperMissileExplosion", 0, frandom(-12.0, 12.0), frandom(-12.0, 12.0), 0, 0, 0, 0, SXF_NOCHECKPOSITION | SXF_TRANSFERPOINTERS)
		TNT1 A 7 A_SpawnItemEx("TangoStomperMissileExplosion", 0, frandom(-12.0, 12.0), frandom(-12.0, 12.0), 0, 0, 0, 0, SXF_NOCHECKPOSITION | SXF_TRANSFERPOINTERS)
		TNT1 A 7 A_SpawnItemEx("TangoStomperMissileExplosion", 0, frandom(-12.0, 12.0), frandom(-12.0, 12.0), 0, 0, 0, 0, SXF_NOCHECKPOSITION | SXF_TRANSFERPOINTERS)
		Stop
	}
}

Actor TangoStomperMissile2 : TangoStomperMissile1
{
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_SetUserVar("user_loops_remaining", 9)
		Goto Super::SpawnLoop
	}
}

Actor TangoStomperMissile3 : TangoStomperMissile1
{
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_SetUserVar("user_loops_remaining", 8)
		Goto Super::SpawnLoop
	}
}


Actor TangoStomperMissileExplosion
{
	Radius 8
	Height 16
	Speed 0
	+NOGRAVITY
	Projectile
	DamageType "Rocket"
	Obituary "$OB_MPROCKET" // "%o rode %k's rocket."
	SeeSound ""
	DeathSound ""
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Jump(160, "SkipSound")
		TNT1 A 0 A_PlaySound("Stomper/explode", CHAN_5)
	SkipSound:
		TNT1 A 0 Radius_Quake(2, 1, 0, 1, 0)
		TNT1 A 0 A_Jump(256, "Explode1", "Explode2", "Explode3")
	Explode1:
		CME1 ABC 2 Bright A_Explode(5, 56, XF_HURTSOURCE, false, 32)
		CME1 DEF 2 Bright
		Stop
	Explode2:
		CME2 ABC 2 Bright A_Explode(5, 56, XF_HURTSOURCE, false, 32)
		CME2 DE 2 Bright
		Stop
	Explode3:
		CME3 ABC 2 Bright A_Explode(5, 56, XF_HURTSOURCE, false, 32)
		CME3 DEF 2 Bright
		Stop
	}
}

Actor TangoStomperMissileTrail
{
	Radius 1
	Height 1
	Speed 0
	Alpha 0.6
	// Scale 0.25
	Scale 0.5
	+NOINTERACTION
	+CLIENTSIDEONLY
	+RANDOMIZE
    States
    {
    Spawn:
		TNT1 A 1
		// SCTR KLMNOPQRSTUVWX 1 Bright
		SMXP ABCDE 2 Bright
		Stop
    }
}