Actor PistolAmmo : Ammo
{
	Game Doom
	Inventory.Amount 8
	Inventory.MaxAmount 8
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 8
	+IGNORESKILL
}

Actor TangoPistol : Weapon replaces Pistol
{
	Tag "Pistol"
	+WEAPON.NOAUTOFIRE
	+WEAPON.AMMO_OPTIONAL
	+WEAPON.NOALERT
	Weapon.SelectionOrder 1900
	decal "bulletchip"
	Weapon.AmmoUse1 1
	Weapon.AmmoUse2 0
	Weapon.AmmoType1 "PistolAmmo"
	Weapon.AmmoType2 "TangoBulletClip"
	Weapon.AmmoGive2 20
	Weapon.SlotNumber 2
	Inventory.PickupMessage "$TANGO_PISTOL"
	States
	{
	Spawn:
		TPIS A -1
		Stop
	Ready:
		PKPI A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Deselect:
		PKPI A 0 A_Lower
		Loop
	Select:
		TNT1 A 0 A_PlaySound("pistol/select", CHAN_WEAPON)
		PKPI A 0 A_Raise
		Loop
	Fire:
	    TNT1 A 0 A_JumpIfInventory("PistolAmmo", 1, 1)
		Goto Reload
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_FireBullets(0, 0, 1, 22, "TBulletPuff", FBF_USEAMMO|FBF_NORANDOM, 8192) // , "BulletTracer", 0)
		TNT1 A 0 A_PlaySound("pistol/fire", CHAN_WEAPON)
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_Light1
	
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("tango_cvar_enable_screen_shake") == 0, "SkipShake")
		TNT1 A 0 Radius_Quake(1, 2, 0, 1, 0)
		SkipShake:
		
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("tango_cvar_enable_recoil") == 0, "SkipRecoil1")
		TNT1 A 0 A_SetPitch(pitch - 0.5)
		SkipRecoil1:
		PKPF A 2 Bright
		
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("tango_cvar_enable_recoil") == 0, "SkipRecoil2")
		TNT1 A 0 A_SetPitch(pitch + 0.25)
		SkipRecoil2:
		
		TNT1 A 0 A_Light0
		// Positional calculations for the casing pulled from complex-doom.v27a5.pk3 - thanks Daedalus :D
		TNT1 A 0 A_SpawnItemEx("9mmCasing", 30 * cos(pitch), 0, 30 - (30 * sin(pitch)), frandom(2, 4) * cos(pitch), frandom(3, 6), frandom(3, 6) * -sin(pitch), 0, SXF_NOCHECKPOSITION | SXF_TRANSFERPITCH)
		PKPI B 1
		
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("tango_cvar_enable_recoil") == 0, "SkipRecoil3")
		TNT1 A 0 A_SetPitch(pitch + 0.25)
		SkipRecoil3:
		
		PKPI CDEFA 1 A_WeaponReady(WRF_ALLOWRELOAD)
		TNT1 A 0 A_JumpIfNoAmmo("CheckAutoReload")
		Goto Ready
	CheckAutoReload:
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("tango_cvar_auto_reload") == 1, "ReloadPreBuffer")
		Goto Ready
	ReloadPreBuffer:
		PKPI A 1 A_WeaponReady(WRF_NOFIRE)
		Goto Reload
	NoAmmo:
		PKPI A 2 A_PlaySound("weapons/empty")
		Goto Ready
	Reload:
		TNT1 A 0 A_JumpIfInventory("TangoBulletClip", 1, 1)
		Goto NoAmmo
		TNT1 A 0 A_JumpIfInventory("PistolAmmo", 0,"Ready")
		TNT1 A 0 A_WeaponReady(WRF_NOBOB)
		PKR2 A 1 A_PlaySound("pistol/reload1", CHAN_AUTO)
		PKR2 BC 1
		PKR2 D 2
		PKR2 E 2 // spawn empty mag
		PKR2 F 5
		PKR2 GHI 2
		PKR2 J 2 A_PlaySound("pistol/reload2", CHAN_AUTO)
		PKR2 KL 2
		PKPR MNOPQ 1
	ReloadRepeat:	
		TNT1 A 0 A_JumpIfInventory("PistolAmmo", 0, "ReloadFinish")
		TNT1 A 0 A_JumpIfInventory("TangoBulletClip", 1, 1)
		Goto ReloadFinish
		TNT1 A 0 A_GiveInventory("PistolAmmo", 1)
		TNT1 A 0 A_Takeinventory("TangoBulletClip",1)
		Goto ReloadRepeat
	ReloadFinish:
		Goto Ready
	}
}