Actor EmptyRoll : Inventory
{
	Inventory.MaxAmount 1
}

Actor TangoMinigun: Weapon 28704
{
	//$Category Weapons
	//$Title "TangoMinigun"
	Tag "Minigun"
	+WEAPON.AMMO_OPTIONAL
	+WEAPON.NOALERT
	Decal "bulletchip"
	Weapon.SlotNumber 6
	Weapon.AmmoUse 1
	Weapon.AmmoType "TangoBulletClip"
	Inventory.PickupSound "minigun/pickup"
	Obituary "%o was gunned down by %k's Minigun."
	Inventory.PickupMessage "$TANGO_MINIGUN"
	Weapon.Kickback 80
	Weapon.AmmoGive 20
	
	States
	{
	Spawn:
		TMGP A -1
		Stop
	Ready:
		TMGN A 1 A_WeaponReady
		Loop
	Fire:
		TNT1 A 0 A_PlaySound("minigun/windup", CHAN_WEAPON)
		TMGN ABC 4
		TMGN DAB 3
		TMGN CDA 2
		TMGN BC 1
		//GPI checks if attack button is being pressed. If so, gives player EmptyRoll.
		TNT1 A 0 ACS_NamedExecuteAlways("tango_get_minigun_input",0)
		TMGN D 1
		TNT1 A 0 A_JumpIfNoAmmo("Empty")
		Goto NormalFire
	Hold:
		TNT1 A 0 A_JumpIfNoAmmo("Empty")
		Goto NormalFire
	NormalFire:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("minigun/fire", CHAN_WEAPON)
		TNT1 A 0 A_Light2
		TNT1 A 0 A_SpawnItemEx("ChaingunCasing", 20 * cos(pitch), 8, 30 - (30 * sin(pitch)), frandom(2, 4) * cos(pitch), frandom(3, 6), frandom(3, 6) * -sin(pitch), 0, SXF_NOCHECKPOSITION | SXF_TRANSFERPITCH)
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("tango_cvar_enable_screen_shake") == 0, "SkipShake")
		TNT1 A 0 Radius_Quake(2, 2, 0, 1, 0)
	SkipShake:
	// A weapon firing this fast is ok without the recoil, I think?
// 		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("tango_cvar_enable_recoil") == 0, "SkipRecoil1")
// 		TNT1 A 0 A_SetPitch(pitch - 0.5)
// 	SkipRecoil1:
		TMGF A 1 A_FireBullets(12, 6, -1, 18, "TBulletPuff", FBF_USEAMMO|FBF_NORANDOM, 8192)
		TNT1 A 0 A_Saw("","", 25, "TangoSawPuff", SF_NORANDOM|SF_NOUSEAMMO)
		TNT1 A 0 A_TakeInventory("EmptyRoll",1)
		TNT1 A 0 A_Jump(128,"NoFlash1")
		TMGF B 1 A_Light1
		Goto FlashDone
	NoFlash1:
		TMGN B 1 A_Light0
	FlashDone:
		TNT1 A 0 A_JumpIfNoAmmo("NoSecondShot")
		TNT1 A 0 A_PlaySound("minigun/fire", CHAN_WEAPON)
		TNT1 A 0 ACS_NamedExecuteAlways("tango_get_minigun_input",0)
		TNT1 A 0 A_Jump(128,"NoFlash2")
		TMGF C 1 A_Light1
		Goto FlashDone2
	NoFlash2:
		TMGN C 1 A_Light0
	FlashDone2:
		TNT1 A 0 A_FireBullets(12, 6, -1, 18, "TBulletPuff", FBF_USEAMMO|FBF_NORANDOM, 8192)
		TMGF D 1 A_Refire
		Goto FinishFire
	NoSecondShot:
		TNT1 A 0 ACS_NamedExecuteAlways("tango_get_minigun_input",0)
		TMGN C 1 A_Light0
		TMGN D 1 A_Refire
		Goto FinishFire
	Empty:
		TNT1 A 0 ACS_NamedExecuteAlways("tango_get_minigun_input",0)
		TNT1 A 0 A_JumpIfInventory("EmptyRoll",1,"EmptyRollState")
		Goto FinishFire
	EmptyRollState:
		TNT1 A 0 A_TakeInventory("EmptyRoll",1)
		TMGN ABCD 1 A_PlaySound("minigun/rotate", CHAN_WEAPON)
		TNT1 A 0 A_Saw("","", 25, "TangoSawPuff", SF_NORANDOM|SF_NOUSEAMMO)
		TNT1 A 0 A_ReFire
		Goto FinishFire
	FinishFire:
		TNT1 A 0 A_TakeInventory("EmptyRoll",1)
		TNT1 A 0 A_ClearReFire
		TNT1 A 0 A_PlaySound("minigun/winddown", CHAN_WEAPON)
		TMGN ABCD 1
		TMGN ABCD 2
		TMGN AB 3
		TMGN CD 4
		TMGN A 4 A_WeaponReady
		TMGN BCD 5 A_WeaponReady
		Goto Ready
	Select:
		TNT1 A 0 A_PlaySound("minigun/pickup", CHAN_WEAPON)
		TMGN AAAAAAAAAAAAAAAA 0 A_Raise
		TMGN A 1 A_Raise
		Loop
	Deselect:
		TNT1 A 0 A_ClearReFire
		TMGN AAAAAAAAAAAAAAAA 0 A_Lower
		TMGN A 1 A_Lower
		Loop
	}
}