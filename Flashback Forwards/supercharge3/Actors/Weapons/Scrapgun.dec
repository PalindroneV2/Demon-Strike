const float scrap_spread = 6.2;
const int scrap_base_vel = 60;

ACTOR ScrapAmmo : Ammo
{
	Inventory.Amount 0
	Inventory.MaxAmount 4
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 4
	+IGNORESKILL
}

// NEW SCRAPGUN
ACTOR TangoScrapgun : Weapon 28700
{
	//$Category Weapons
	//$Title "TangoScrapgun"
	Tag "Scrapgun"
	+WEAPON.AMMO_OPTIONAL
	+WEAPON.NOALERT
	decal "bulletchip"
	Weapon.KickBack 150
	Weapon.SlotNumber 4
	Weapon.AmmoUse1 1
	Weapon.AmmoUse2 0
	Weapon.AmmoType1 "ScrapAmmo"
	Weapon.AmmoType2 "TangoScrapDrum"
	Weapon.AmmoGive1 0
	Weapon.AmmoGive2 8
	Inventory.PickupSound "items/scrapgun"
	Inventory.PickupMessage "$TANGO_SCRAP_GUN"
	DamageType Shotgun
	States
	{
	Spawn:
		SGPU A -1
		Stop
	Ready:
		SCRA A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Deselect:
		SCRA A 0 A_Lower
		Loop
	Select:
		TNT1 A 0 A_PlaySound("items/scrapgun", CHAN_WEAPON)
		SCRA A 0 A_Raise
		Loop
	Fire:
		TNT1 A 0 A_JumpIfNoAmmo("Reload")
		
		TNT1 A 0 A_FireCustomMissile("ScrapSpawner", 0, 1, 0, 8, true)
		TNT1 A 0 A_PlaySound("weapons/scrapfire", CHAN_WEAPON)
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_Light1
		
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("tango_cvar_enable_screen_shake") == 0, "SkipShake")
		TNT1 A 0 Radius_Quake(6, 3, 0, 1, 0)
		SkipShake:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("tango_cvar_enable_recoil") == 0, "SkipRecoil1")
		TNT1 A 0 A_SetPitch(pitch - 1.0)
		SkipRecoil1:
		Goto FinishFire
	FinishFire:
		SCRA A 1 Bright
		SCRA B 1 Bright A_Light2
		TNT1 A 0 A_Light0
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("tango_cvar_enable_recoil") == 0, "SkipRecoil2")
		TNT1 A 0 A_SetPitch(pitch + 0.5)
		SkipRecoil2:
		SCRA C 1
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("tango_cvar_enable_recoil") == 0, "SkipRecoil3")
		TNT1 A 0 A_SetPitch(pitch + 0.5)
		SkipRecoil3:
		SCRA D 1
		SCRA EFF 1
		SCRA I 2 A_PlaySound("scrapgun/rotate", CHAN_AUTO)
		SCRA GGHHH 1
		TNT1 A 0 A_JumpIfNoAmmo("CheckAutoReload")
		Goto Ready
	CheckAutoReload:
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("tango_cvar_auto_reload") == 1, "ReloadPreBuffer")
		Goto Ready
	ReloadPreBuffer:
		SCRA A 1 A_WeaponReady(WRF_NOFIRE)
		Goto Reload
	NoAmmo:
		SCRA A 2 A_PlaySound("weapons/empty")
		Goto Ready
	
	Reload:
		TNT1 A 0 A_JumpIfInventory("ScrapAmmo", 0, "Ready")
		TNT1 A 0 A_JumpIfInventory("TangoScrapDrum", 1, 1)
		Goto NoAmmo
		SRLA ABCFE 1
		SRLA D 1 A_PlaySound("weapons/scrapopen", CHAN_AUTO)
		SRLB ABCDE 2
		SRLB F 1 A_PlaySound("scrapgun/eject", CHAN_WEAPON)
		SRLB GHI 1
		SRLB J 7
		SRLB KLM 2
		SRLB NO 1
		SRLB P 3 A_PlaySound("weapons/scrapload", CHAN_AUTO)
	ReloadRepeat:
		// if full on scrap ammo, finish reload
		TNT1 A 0 A_JumpIfInventory("ScrapAmmo", 0, "ReloadFinish")
		// if at least 1 shell to load, continue
		TNT1 A 0 A_JumpIfInventory("TangoScrapDrum", 1, 1)
		Goto ReloadFinish
		// exchange scrap for shell
		TNT1 A 0 A_GiveInventory("ScrapAmmo", 1)
		TNT1 A 0 A_Takeinventory("TangoScrapDrum",1)	
		Goto ReloadRepeat
	ReloadFinish:
		SRLB N 2 A_PlaySound("weapons/scrapclose", CHAN_AUTO)
		SRLB Q 2
		SRLB R 2
		SRLB ST 1
		SRLA D 2
		SRLA B 2
		SCRA A 1
		SCRA H 3
		SCRA A 3
		Goto Ready
	}
}

// =========



ACTOR Scrap : FastProjectile
{
	// Projectile
	Damage (6)
	Decal "ScrapChip"
	Speed 60
	Height 4
	Radius 2
	+BLOODSPLATTER
	Scale 0.25
	DamageType Shotgun
	States
	{
	Spawn:
		TNT1 A 0
		SHRD AABBCCDDEEFFGGHH 1 A_SpawnItemEx("ScrapTrail", 0, 0, 0, 0, 0, 0)
		Loop
	Death:
		TNT1 A 0 A_SpawnItemEx("TBulletPuff", 0, 0, 0, 0, 0, 0) //A_PlaySound("sfx/nailric")
		Stop
	XDeath:
		TNT1 A 0
		Stop
	}
}

Actor ScrapTrail
{
	Radius 1
	Height 1
	Speed 0
	Alpha 0.6
	Scale 0.25
	+NOINTERACTION
	+CLIENTSIDEONLY
	+RANDOMIZE
    States
    {
    Spawn:
		SCTR KLMNOPQRSTUVWX 1 Bright
		Stop
    }
}

Actor ScrapSpawner
{
	Projectile
	MissileHeight 0
	Radius 1
	Height 2
	Speed 1
	Decal "ScrapChip"
	+BLOODSPLATTER
	+NOEXTREMEDEATH
	
	States
	{
	XDeath:
		TNT1 A 0
		Stop
	Spawn:
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAA 0 NoDelay A_SpawnItemEx("Scrap", 0, 0, 0, (velx * scrap_base_vel) + frandom(-scrap_spread, scrap_spread), (vely * scrap_base_vel) + frandom(-scrap_spread, scrap_spread), (velz * scrap_base_vel) + frandom(-scrap_spread, scrap_spread), 0, SXF_ABSOLUTEMOMENTUM | SXF_TRANSFERPOINTERS)
		Stop
			
	Death:
		Stop
	}
}