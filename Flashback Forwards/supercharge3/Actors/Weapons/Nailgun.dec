const float nailgun_pitch_distance = 0.5;
const float nailgun_super_pitch_distance = 0.8;

Actor NailsFiredCount : Inventory
{
	Inventory.MaxAmount 8
}

ACTOR NailAmmo : Ammo
{
	Inventory.Amount 9
	Inventory.MaxAmount 9
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 9
}

Actor TangoNailgun : Weapon 28711
{
	//$Category Weapons
	//$Title "TangoNailgun"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 25
	Weapon.KickBack 40
	Weapon.AmmoType1 "NailAmmo"
	Weapon.AmmoType2 "TangoNails"
	Weapon.SlotNumber 4
	+WEAPON.AMMO_OPTIONAL
	Inventory.PickupSound "Misc/W_PkUp"
	Inventory.PickupMessage "$TANGO_NAILGUn"
	Obituary "%o was nailed by %k."
	States
	{
	Spawn:
		NGUN A -1
		Loop

	Deselect:
		NLMG A 0 A_Lower
		Loop
	Select:
		TNT1 A 0 A_PlaySound("items/scrapgun", CHAN_WEAPON)
		TNT1 A 0 A_TakeInventory("NailsFiredCount")
		NLMG A 0 A_Raise
		Loop
	Ready:
		NLMG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop

	NoAmmo:
		NLMG A 2 A_PlaySound("weapons/empty")
		Goto Ready

	Fire:
		TNT1 A 0 A_JumpIfNoAmmo("Reload")
		TNT1 A 0 A_JumpIfInventory("NailsFiredCount", 8, "FireSpecial")

		FireLeft:
		TNT1 A 0 A_GiveInventory("NailsFiredCount", 1)

		TNT1 A 0 A_PlaySound("weapons/nailfire", CHAN_AUTO)
		TNT1 A 0 Radius_Quake(2, 2, 0, 1, 0)
		TNT1 A 0 A_SetPitch(pitch - nailgun_pitch_distance)
		NLMG B 2 A_FireCustomMissile("NailProjectile", 0, 1, -6, 0, 0, 0)
		TNT1 A 0 A_SetPitch(pitch + (nailgun_pitch_distance / 2))
		NLMG B 0 Bright A_Light2
		NLMG C 1
		TNT1 A 0 A_SetPitch(pitch + (nailgun_pitch_distance / 2))
		NLMG A 1


		FireRight:
		TNT1 A 0 A_GiveInventory("NailsFiredCount", 1)

		TNT1 A 0 A_PlaySound("weapons/nailfire", CHAN_AUTO)
		TNT1 A 0 Radius_Quake(2, 2, 0, 1, 0)
		TNT1 A 0 A_SetPitch(pitch - nailgun_pitch_distance)
		NLMG D 2 A_FireCustomMissile("NailProjectile", 0, 1, 6, 0, 0, 0)
		TNT1 A 0 A_SetPitch(pitch + (nailgun_pitch_distance / 2))
		NLMG D 0 Bright A_Light2
		NLMG E 1
		TNT1 A 0 A_SetPitch(pitch + (nailgun_pitch_distance / 2))
		NLMG A 2
		NLMG E 0 A_Light0
		NLMG A 1 A_Refire
		Goto Ready


	Reload:
		TNT1 A 0 A_JumpIfInventory("NailAmmo", 0, "Ready")
		TNT1 A 0 A_JumpIfInventory("TangoNails", 1, 1)
		Goto NoAmmo


	ReloadRepeat:
		// if full on scrap ammo, finish reload
		TNT1 A 0 A_JumpIfInventory("NailAmmo", 0, "Ready")
		// if at least 1 shell to load, continue
		TNT1 A 0 A_JumpIfInventory("TangoNails", 1, 1)
		Goto Ready
		// exchange scrap for shell
		TNT1 A 0 A_GiveInventory("NailAmmo", 1)
		TNT1 A 0 A_Takeinventory("TangoNails",1)
		// Only take a single NailsFiredCount at a time, to account for cases
		// where player does partial reload then keeps firing
		TNT1 A 0 A_TakeInventory("NailsFiredCount", 1)
		TNT1 A 0 A_PlaySound("weapons/nailrotate")
		NLMG A 1 Offset(0, 38)
		NLMG A 1 Offset(0, 35)
		NLMG A 1 Offset(0, 33)

		TNT1 A 0 A_JumpIfInventory("NailAmmo", 4, "CheckInterrupt")
		Goto ReloadRepeat
	CheckInterrupt:
		TNT1 A 0 A_WeaponReady(WRF_NOBOB)
		Goto ReloadRepeat

	FireSpecial:
		TNT1 A 0 A_TakeInventory("NailsFiredCount")
		TNT1 A 0 A_Recoil(2.0)

		TNT1 A 0 A_PlaySound("weapons/nailfiresuper", CHAN_AUTO)
		TNT1 A 0 Radius_Quake(4, 2, 0, 1, 0)
		TNT1 A 0 A_SetPitch(pitch - nailgun_super_pitch_distance)
		TNT1 A 0 A_FireCustomMissile("RicochetNail", 0, 1, 0, 0, 0, 0)
		TNT1 A 0 A_FireCustomMissile("RicochetNail", 0, 0, 0, 0, 0, -2)
		TNT1 A 0 A_FireCustomMissile("RicochetNail", 0, 0, 0, 0, 0, 2)
		TNT1 A 0 A_FireCustomMissile("RicochetNail", 2, 2, 0, 0, 0, 0)
		TNT1 A 0 A_FireCustomMissile("RicochetNail", -2, -2, 0, 0, 0, 0)
		
		NLMG F 2 // A_FireCustomMissile("SuperNailProjectile", 0, 1, 0, 0, 0, 0)
		TNT1 A 0 A_SetPitch(pitch + (nailgun_super_pitch_distance / 2))
		TNT1 A 0 Bright A_Light2
		NLMG G 1
		TNT1 A 0 A_SetPitch(pitch + (nailgun_super_pitch_distance / 2))

		NLMG A 22
		TNT1 A 0 A_JumpIfNoAmmo("CheckAutoReload")
		Goto Ready
	CheckAutoReload:
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("tango_cvar_auto_reload") == 1, "ReloadPreBuffer")
		Goto Ready
	ReloadPreBuffer:
		NLMG A 1 A_WeaponReady(WRF_NOFIRE)
		Goto Reload
	}
}

Actor SuperNailProjectile : FastProjectile
{
	Radius 8
	Height 16
	Speed 100
	Damage (8)
	Scale 0.5
	Projectile
	+RANDOMIZE
	+RIPPER
	SeeSound "pyro/comet"
	DeathSound "behemoth/cometdeath"
	Damagetype "PyroFire"
	States
	{
	Spawn:
		PCMT AAABBBCCC 1 Bright //A_SpawnItemEx("BehemothCometTrail")
		Loop
	Death:
		//TNT1 A 0 A_SpawnItemEx("PyroDemonCometChunk", 0, 0, 0, random(-1, -7), random(-3, 3), random(4, 12), 0, SXF_TRANSFERPOINTERS)
		TNT1 A 0 A_Explode(64, 64)
		TNT1 A 0 A_CustomMissile("RicochetNail", 0, 0, 0, 0, -pitch)
		TNT1 A 0 A_CustomMissile("RicochetNail", 0, 0, -2, 0, -pitch)
		TNT1 A 0 A_CustomMissile("RicochetNail", 0, 0, 2, 0, -pitch)
		TNT1 A 0 A_CustomMissile("RicochetNail", 0, 0, 0, 0, -pitch + 1)
		TNT1 A 0 A_CustomMissile("RicochetNail", 0, 0, 0, 0, -pitch - 1)

// 		TNT1 A 0 A_SpawnItemEx("PyroDemonCometChunk", 0, 0, 0, x1 + 1, y1, z1, 0, SXF_TRANSFERPOINTERS)
// 		TNT1 A 0 A_SpawnItemEx("PyroDemonCometChunk", 0, 0, 0, x2 + 1, y1, z2, 0, SXF_TRANSFERPOINTERS)
// 		TNT1 A 0 A_SpawnItemEx("PyroDemonCometChunk", 0, 0, 0, x3 + 1, y1, z3, 0, SXF_TRANSFERPOINTERS)

// 		TNT1 A 0 A_SpawnItemEx("PyroDemonCometChunk", 0, 0, 0, x1 + 1, y2, z1, 0, SXF_TRANSFERPOINTERS)
// 		TNT1 A 0 A_SpawnItemEx("PyroDemonCometChunk", 0, 0, 0, x2 + 1, y2, z2, 0, SXF_TRANSFERPOINTERS)
// 		TNT1 A 0 A_SpawnItemEx("PyroDemonCometChunk", 0, 0, 0, x3 + 1, y2, z3, 0, SXF_TRANSFERPOINTERS)

// 		TNT1 A 0 A_SpawnItemEx("PyroDemonCometChunk", 0, 0, 0, x1, y3, z1, 0, SXF_TRANSFERPOINTERS)
// 		TNT1 A 0 A_SpawnItemEx("PyroDemonCometChunkWithSound", 0, 0, 0, x2, y3, z2, 0, SXF_TRANSFERPOINTERS)
// 		TNT1 A 0 A_SpawnItemEx("PyroDemonCometChunk", 0, 0, 0, x3, y3, z3, 0, SXF_TRANSFERPOINTERS)


		PCMD ABCDEFGHIJKLMN 2 Bright
		Stop
	}
}

Actor NailProjectile : FastProjectile
{
	Projectile
	Radius 3
	Height 3
	Speed 85
	Damage (14)
	+BloodSplatter
	SeeSound ""
	DeathSound ""
	Decal BulletChip
	States
	{
	Spawn:
		NAIL B 1 Bright A_SpawnItemEx("NailTrail", 0, 0, 0, 1, 0, 0, 180, 128)
		NAIL A 0 A_PlaySound("Weapons/NailFlight")
		Loop
	Crash:
	Death:
		TNT1 A 0 A_PlaySound("Weapons/NailRicochet")
		TNT1 A 0 A_SpawnItemEx("NailRicochet", -16, 0, 0, frandom(-2.0, 2.0), 0, frandom(2.0, 4.0), 0, 0, 0, 0)
		Stop
	XDeath:
		TNT1 A 0 A_PlaySound("Weapons/NailHitBleed")
		NAIL DEFGHI 2 Bright
		Stop
	}
}

Actor RicochetNail
{
	Projectile
	Radius 3
	Height 3
	Speed 60
	Damage (60)
	+BloodSplatter
	SeeSound ""
	DeathSound ""
	Decal BulletChip
	BounceType Hexen
	BounceFactor 1.0
	WallBounceFactor 1.0
	BounceCount 6
	BounceSound "Weapons/NailRicochet"
	States
	{
	Spawn:
		NAIL C 1 Bright A_SpawnItemEx("NailTrail", 12, 0, 0, 1, 0, 0, 180, 128)
		NAIL A 0 A_PlaySound("Weapons/NailFlight")
		Loop
	Crash:
	Death:
		TNT1 A 0 A_PlaySound("Weapons/NailRicochet")
		TNT1 A 0 A_SpawnItemEx("NailRicochet", -16, 0, 0, frandom(-2.0, 2.0), 0, frandom(2.0, 4.0), 0, 0, 0, 0)
		Stop
	XDeath:
		TNT1 A 0 A_PlaySound("Weapons/NailHitBleed")
		NAIL DEFGHI 2 Bright
		Stop
	}
}

Actor NailRicochet {
	Radius 14
	Height 28
	Gravity 0.4
	Scale 0.5
	+THRUACTORS
	+CLIENTSIDEONLY
	+DONTSPLASH
	+MISSILE
	BounceType Doom
	BounceFactor 0.5
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Jump(256, "Clockwise", "CounterClockwise")
		Goto Clockwise
	Clockwise:
		NAIR HABCDEFG 2
		Loop
	CounterClockwise:
		NAIR FEDCBAHG 2
		Loop
	Death:
		TNT1 A 0 A_Jump(256, "StillLeft", "StillRight")
		Goto StillLeft
	StillLeft:
		NAIR A -1
		Stop
	StillRight:
		NAIR E -1
		Stop
	}
}

Actor NailTrail       
{
	Height 8
	Radius 1
	Damage 0
	Speed 0.2
	RenderStyle Translucent
	Alpha 1.0
	+NoGravity
	+DropOff
	+NoTeleport
	States
	{
	Spawn:
		NTRA A 1 A_FadeOut(0.1)
		Loop
	}
}
