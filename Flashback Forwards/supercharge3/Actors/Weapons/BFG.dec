Actor TangoBFG9000 : BFG9000 replaces BFG9000
{	
	Tag "BFG"
	+WEAPON.NOALERT
	+WEAPON.AMMO_OPTIONAL
	-WEAPON.NOAUTOFIRE
	Weapon.AmmoType "TangoCell"
	Weapon.SlotNumber 7
	Inventory.PickupMessage "$TANGO_BFG"
	States
	{
	Spawn:
		BFUG A -1
		Stop
	Select:
		TNT1 A 0 A_PlaySound("bfg/select", CHAN_WEAPON)
		BFGG A 0 A_Raise
		Loop
	Deselect:
		BFGG A 0 A_Lower
		Loop
	Ready:
		BFGG ABC 4 A_WeaponReady
		BFGG D 16 A_WeaponReady
		BFGG CB 4 A_WeaponReady
		Loop
	NoAmmo:
		BFGG A 2 A_PlaySound("weapons/empty")
		Goto Ready
	Fire:
		TNT1 A 0 A_JumpIfInventory("TangoCell", 40, 1)
        Goto NoAmmo
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("bfg/charge", CHAN_WEAPON)
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("tango_cvar_enable_screen_shake") == 0, "ChargeNoShake")
		Goto ChargeShake
	
	ChargeShake:
		BFGG D 4
		BFGG C 2 Offset(-1, 33)
	FastChargeShake:
		BFGG C 2
		BFGG B 2 Offset(1, 33) Radius_Quake(1, 1, 0, 1, 0)
		BFGG B 2 Offset(-1, 34)
		
		BFGG A 1 Offset(-1, 33) Radius_Quake(1, 1, 0, 1, 0)
		BFGG A 1
		BFGG A 1 Offset(1, 33)
		BFGG A 1
		
		BFGG A 1 Offset(-1, 33) Radius_Quake(1, 1, 0, 1, 0)
		BFGG A 1
		BFGG A 1 Offset(1, 33)
		BFGG A 1
		BFGG A 1 Offset(-1, 33) Radius_Quake(1, 2, 0, 1, 0)
		BFGG A 1
		
		BFGG A 1 Offset(-1, 33) Radius_Quake(1, 2, 0, 1, 0)
		BFGG A 1
		BFGG A 1 Offset(1, 34)
		BFGG A 1 
		BFGG A 1 Offset(-1, 33) Radius_Quake(1, 2, 0, 1, 0)
		BFGG A 1 
		BFGG A 1 Offset(1, 33)
		BFGG A 1 Offset(0, 32)
		Goto FireBFG
		
	ChargeNoShake:
		BFGG D 4
		BFGG C 2 Offset(-1, 33)
	FastChargeNoShake:
		BFGG C 2
		BFGG B 2 Offset(1, 33)
		BFGG B 2 Offset(-1, 34)
		
		BFGG A 1 Offset(-1, 33)
		BFGG A 1
		BFGG A 1 Offset(1, 33)
		BFGG A 1
		
		BFGG A 1 Offset(-1, 33)
		BFGG A 1
		BFGG A 1 Offset(1, 33)
		BFGG A 1
		BFGG A 1 Offset(-1, 33)
		BFGG A 1
		
		BFGG A 1 Offset(-1, 33)
		BFGG A 1
		BFGG A 1 Offset(1, 34)
		BFGG A 1 
		BFGG A 1 Offset(-1, 33)
		BFGG A 1 
		BFGG A 1 Offset(1, 33)
		BFGG A 1 Offset(0, 32)
		Goto FireBFG
		// 30
	FireBFG:
		TNT1 A 0 A_StopSound(CHAN_WEAPON)
		TNT1 A 0 A_FireCustomMissile("TangoBFGBall", 0, 1)
		
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("tango_cvar_enable_screen_shake") == 0, "SkipShake1")
		TNT1 A 0 Radius_Quake(5, 4, 0, 1, 0)
		
		SkipShake1:
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("tango_cvar_enable_recoil") == 0, "SkipRecoil1")
		TNT1 A 0 A_SetPitch(pitch - 1.4)
		
		SkipRecoil1:
		BFGR B 2 Bright A_Light2
		
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("tango_cvar_enable_recoil") == 0, "SkipRecoil2")
		TNT1 A 0 A_SetPitch(pitch + 0.7)
		SkipRecoil2:
		
		BFGR A 1 Bright A_Light1
		
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("tango_cvar_enable_recoil") == 0, "SkipRecoil3")
		TNT1 A 0 A_SetPitch(pitch + 0.7)
		SkipRecoil3:
		
		BFGR A 1 Bright
		BFGR B 3 Bright
		BFGR C 2 Bright A_Light0
		BFGR D 2 
		BFGR EF 2
		BFGG A 19 A_ReFire
    	goto Ready
	Hold:
		TNT1 A 0 A_JumpIfInventory("TangoCell",40,1)
        Goto NoAmmo
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("bfg/charge", CHAN_WEAPON)
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("tango_cvar_enable_screen_shake") == 0, "FastChargeNoShake")
		Goto FastChargeShake
	}
}

Actor TangoBFGBall replaces BFGBall
{
	Damage (400)
	Radius 16
	Height 16
	Speed 25
	Projectile
	+RANDOMIZE
	RenderStyle "Normal"
	SeeSound "bfg/fire"
	DeathSound "weapons/bfgx"
	Obituary "$OB_MPBFG_BOOM"
	DamageType "bfg"
	States
	{
	Spawn:
		TBBS AAABBB 1 Bright A_SpawnItemEx("TangoBFGBallTrail", frandom(-4.0, 4.0), frandom(-4.0, 4.0), frandom(-4.0, 4.0), frandom(-0.5, 0.5), 0, frandom(-1.0, 1.0))
		Loop
	Death:
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("TangoBFGBallSpark", 0, frandom(-18.0, 18.0), frandom(-18.0, 18.0), frandom(0.5, 3.0), frandom(-3.0, 3.0), frandom(-4.0, 4.0), 0, SXF_NOCHECKPOSITION)
		TBBD ABCD 3 Bright
		TBBD F 1 Bright
		TBBD F 3 Bright A_BFGSpray("TangoBFGExtra")
		TBBD GHIJ 3 Bright
		Stop
	}
}

Actor TangoBFGBallTrail
{
	Radius 1
	Height 1
	Speed 0
	Alpha 0.6
	Scale 0.8
	Renderstyle "Add"
	+NOINTERACTION
	+CLIENTSIDEONLY
    States
    {
    Spawn:
		TNT1 A 3
	Death:
		SPLS IJKL 2 Bright
		Stop
    }
}

Actor TangoBFGBallSpark
{
	Scale 0.4
	Radius 2
	Height 2
	Speed 1
	Projectile
	Gravity 0.2
	-NOGRAVITY
	-SOLID
	+DOOMBOUNCE
	+EXPLODEONWATER
	-CANBOUNCEWATER
	+DONTSPLASH
	+NOTELEPORT
	+CLIENTSIDEONLY

	States
	{
	Spawn:
		SPLS IJKLIJKL 1 Bright
	FadeOut:
		SPLS IJKL 2 Bright A_FadeOut(0.15)
		Loop
	}
}

Actor TangoBFGExtra
{
  +NOBLOCKMAP
  +NOGRAVITY
  RenderStyle Add
  Alpha 0.75
  DamageType "BFGSplash"
  States
  {
  Spawn:
    TBEX ABCDEFG 3 Bright
	TBEX HIJKLMNO 2 Bright
    Stop
  }
}