Actor TangoRocketLauncher : RocketLauncher replaces RocketLauncher
{
	Tag "Rocket Launcher"
	+WEAPON.NOALERT
	+WEAPON.AMMO_OPTIONAL
	-WEAPON.NOAUTOFIRE
	Weapon.SlotNumber 5
	Weapon.SelectionOrder 50
	Weapon.AmmoType1 "TangoRocketAmmo"
	Inventory.PickupMessage "$TANGO_ROCKET_LAUNCHER"
	Weapon.KickBack 120
	States
	{
	Spawn:
		LAUN A -1
		Stop
	Select:
		TNT1 A 0 A_PlaySound("rocketlauncher/select", CHAN_WEAPON)
		PKRL A 0 A_Raise
		Loop
	Deselect:
		PKRL A 0 A_Lower
		Loop
	Ready:
		PKRL A 1 A_WeaponReady
		Loop
	NoAmmo:
		PKRL A 2 A_PlaySound("weapons/empty")
		Goto Ready
	Fire:
		TNT1 A 0 A_JumpIfInventory("TangoRocketAmmo", 1, 1)
        Goto NoAmmo
		TNT1 A 0 A_AlertMonsters
		PKRL A 3 A_GunFlash
		TNT1 A 0 A_PlaySound("weapons/rocklf", CHAN_WEAPON)
		TNT1 A 0 A_FireCustomMissile("TangoWeaponRocket")
		
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("tango_cvar_enable_screen_shake") == 0, "SkipShake")
		TNT1 A 0 Radius_Quake(2, 2, 0, 1, 0)
		SkipShake:
		
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("tango_cvar_enable_recoil") == 0, "SkipRecoil1")
		TNT1 A 0 A_SetPitch(pitch - 0.8)
		SkipRecoil1:
		
		
		PKRL B 2
		
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("tango_cvar_enable_recoil") == 0, "SkipRecoil2")
		TNT1 A 0 A_SetPitch(pitch + 0.4)
		SkipRecoil2:
		
		PKRL D 1
		
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("tango_cvar_enable_recoil") == 0, "SkipRecoil3")
		TNT1 A 0 A_SetPitch(pitch + 0.4)
		SkipRecoil3:
		
		PKRL D 2
		PKRL C 3
		PKRL BE 2
		PKRL FG 1
		PKRL A 1
		PKRL A 1 A_Refire
		Goto Ready
	Flash:
		PKRF A 1 Bright A_Light1
		PKRF B 2 Bright
		PKRF C 2 Bright A_Light2
		PKRF DE 3 Bright
		Goto LightDone
	
	}
}

// player-fired rockets
Actor TangoWeaponRocket : FastProjectile
{
	Radius 8
	Height 12
	Projectile
	Damage (116)
	Speed 40
	DamageType "Rocket"
	MissileType "TangoWeaponRocketSparkTrailSpawner"
	MissileHeight 8
	// SeeSound "weapons/rocklf"
	DeathSound "weapons/prockxp"
	Obituary "$OB_MPROCKET" // "%o rode %k's rocket."
	States
	{
	Spawn:
		DROK A 1 Bright
		Loop
	Death:
		// If this jump is here then the screen shake never happens, and I can't figure out why,
		// so I'll deal with this in a future version I guess
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("tango_cvar_rocket_screen_shake") == 0, "SkipShake")
		TNT1 A 0 Radius_Quake(2, 6, 0, 5, 0)
		
		SkipShake:
		TNT1 A 0 A_Explode(84, 96, XF_HURTSOURCE, false, 40)
		TNT1 AAAA 0 A_SpawnItemEx("TangoWeaponRocketSpark", 0, 0, 0, frandom(-6.0, 6.0), frandom(-6.0, 6.0), frandom(-2.0, 5.0), 0)
		DRKE A 1 Bright
		TNT1 AAAA 0 A_SpawnItemEx("TangoWeaponRocketSpark", 0, 0, 0, frandom(-6.0, 6.0), frandom(-6.0, 6.0), frandom(-2.0, 5.0), 0)
		DRKE A 1 Bright
		TNT1 AAAA 0 A_SpawnItemEx("TangoWeaponRocketSpark", 0, 0, 0, frandom(-6.0, 6.0), frandom(-6.0, 6.0), frandom(-2.0, 5.0), 0)
		DRKE B 2 Bright
		DRKE CDEF 2 Bright
		Stop
	}
}

Actor TangoWeaponRocketSpark
{
	// Scale 0.15
	Scale 0.5
	Radius 2
	Height 2
	BounceType Doom
	Gravity 0.2
	+THRUACTORS
	+CLIENTSIDEONLY
	+DONTSPLASH
	States
	{
	Spawn:
		SMXP AAABBBBBBCDE 1 Bright A_SpawnItemEx("TangoWeaponRocketSparkTrail")
		//SCTR KLMNOPQRSTUVWX 1 Bright A_SpawnItemEx("TangoWeaponRocketSparkTrail")
		Stop
	}
}

Actor TangoWeaponRocketSparkTrailSpawner
{
	+NOGRAVITY
	+CLIENTSIDEONLY
	+NOINTERACTION
	+DONTSPLASH
	States
	{
	Spawn:
		TNT1 A 0
		// Chance to do nothing to make the trail a little less thick
		TNT1 A 0 A_Jump(96, "DoNothing")
		TNT1 A 0 A_SpawnItemEx("TangoWeaponRocketSparkTrail", -12, 0, 0, frandom(-1.5, 1.5), frandom(-1.0, 1.0), velz + frandom(-1.0, 1.0), 0)
		Stop
	DoNothing:
		Stop		
	}
}

Actor TangoWeaponRocketSparkTrail
{
	// Scale 0.15
	Scale 0.5
	+NOGRAVITY
	+CLIENTSIDEONLY
	+NOINTERACTION
	+DONTSPLASH
	States
	{
	Spawn:
		TNT1 A 0
		SMXP A 1 Bright
		SMXP B 1 Bright
		SMXP C 1 Bright
		SMXP D 1 Bright
		Stop
	}
}


// Rocket launcher with alt sprites, unused
Actor TangoRocketLauncherAlt : RocketLauncher // replaces RocketLauncher
{
	Tag "Rocket Launcher"
	+WEAPON.NOALERT
	+WEAPON.AMMO_OPTIONAL
	-WEAPON.NOAUTOFIRE
	// Weapon.SlotNumber 5
	Weapon.SelectionOrder 50
	Weapon.AmmoType1 "TangoRocketAmmo"
	Inventory.PickupMessage "$TANGO_ROCKET_LAUNCHER"
	Weapon.KickBack 120
	States
	{
	Spawn:
		TRLP A -1
		Stop
	Select:
		TNT1 A 0 A_PlaySound("rocketlauncher/select", CHAN_WEAPON)
		TRLA A 0 A_Raise
		Loop
	Deselect:
		TRLA A 0 A_Lower
		Loop
	Ready:
		TRLA A 1 A_WeaponReady
		Loop
	NoAmmo:
		TRLA A 2 A_PlaySound("weapons/empty")
		Goto Ready
	Fire:
		TNT1 A 0 A_JumpIfInventory("TangoRocketAmmo", 1, 1)
        Goto NoAmmo
		TRLF A 2
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("rocketlauncher/fire", CHAN_WEAPON)
		TNT1 A 0 A_FireCustomMissile("TangoWeaponRocket")
		
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("tango_cvar_enable_screen_shake") == 0, "SkipShake")
		TNT1 A 0 Radius_Quake(4, 2, 0, 1, 0)
		SkipShake:
		
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("tango_cvar_enable_recoil") == 0, "SkipRecoil1")
		TNT1 A 0 A_SetPitch(pitch - 1.2)
		SkipRecoil1:
		
		TRLF B 2 
		
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("tango_cvar_enable_recoil") == 0, "SkipRecoil2")
		TNT1 A 0 A_SetPitch(pitch + 0.6)
		SkipRecoil2:
		
		TRLF C 2
		
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("tango_cvar_enable_recoil") == 0, "SkipRecoil3")
		TNT1 A 0 A_SetPitch(pitch + 0.6)
		SkipRecoil3:
		
		TRLF D 2
		TRLF E 2
		TRLF F 2
		TRLA A 6 // 18 at end of here
		TRLA A 1 A_Refire
		Goto Ready
	
	}
}

