/////////////////////////////
// SPARKLE FOUNTAINS ////////
// for UTNT          ////////
// by Tormentor667   ////////
/////////////////////////////

Actor TangoBaseStar
{
 +Missile
 +NoGravity
 +NoBlockmap
 +NoInteraction
 -Solid
 Radius 0
 Height 0
 RenderStyle Add
 Alpha 0.01
 Scale 0.20
 States
 {
  Spawn:
   PTCL AABBCCAA 1 Bright A_FadeIn(0.2)
   PTCL AABBCCAABBCCAABBCC 1 Bright A_FadeOut(0.06)
   Stop
 }
}

Actor TangoBlueStar : TangoBaseStar
{
	Scale 0.10
	States
	{
	Spawn:
	PTCB AABBCCAA 1 Bright A_FadeIn(0.2)
	PTCB AABBCCAABBCCAABBCC 1 Bright A_FadeOut(0.06)
	Stop
 }
}

/*
	ARGUMENTS TO THE SPARKLE SPAWNERS:
	
	0: x spawn offset
	1: whether to spawn in circle or not
	2: spawn failure chance; 0 = always spawn, 255 = never spawn
	3: z velocity; can be set to negative for upside down spawners
	
	Specifying arguments is not possible in Doom format maps, so if you intend
	to use these sparkles in Doom format maps, you'll have to make copies of
	these sparkle actors or modify them so that all the Arg[x] values are hard
	coded.
*/
actor TangoWhiteSparkleSpawner 29401
{
	//$Category "Supercharge Special Effects"
	//$Color 5
	//$Sprite PTCLA0
	//$Icon PTCLA0
	+NoBlockMap
	+NoGravity
	+NoSector
	+NoInteraction
	+NoClip
	-Solid
	+CLIENTSIDEONLY
	states
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_JumpIf(Args[1], "Circle")
		TNT1 A 0 A_JumpIf(Args[3] < 0, "ActiveUpsideDown")
	Active:
		TNT1 A 4 A_SpawnItemEx("TangoBaseStar", random(-Args[0], Args[0]), random(-Args[0], Args[0]), random(0, 8), 0, 0, random(1, Args[3]), 0, SXF_TRANSFERTRANSLATION, Args[2]) 
		Loop
	ActiveUpsideDown:
		TNT1 A 0
		TNT1 A 0 A_JumpIf(Args[1], "Circle")
		TNT1 A 4 A_SpawnItemEx("TangoBaseStar", random(-Args[0], Args[0]), random(-Args[0], Args[0]), random(0, 8), 0, 0, random(-1, Args[3]), 0, SXF_TRANSFERTRANSLATION, Args[2]) 
		Loop
	Circle:
		TNT1 A 0 A_JumpIf(Args[3] < 0, "CircleUpsideDown")
	CircleLoop:
		TNT1 A 0
		TNT1 A 4 A_SpawnItemEx("TangoBaseStar", random(-Args[0], Args[0]), 0, random(0, 8), 0, 0, random(1, Args[3]), Random(0, 360), SXF_TRANSFERTRANSLATION, Args[2]) 
		Loop
	CircleUpsideDown:
		TNT1 A 0
		TNT1 A 4 A_SpawnItemEx("TangoBaseStar", random(-Args[0], Args[0]), 0, random(0, 8), 0, 0, random(-1, Args[3]), Random(0, 360), SXF_TRANSFERTRANSLATION, Args[2]) 
		Loop
	Inactive:
		TNT1 A 1
		Loop
	}
}

actor TangoBlueSparkleSpawner : TangoWhiteSparkleSpawner 29402
{
	//$Category "Supercharge Special Effects"
	//$Color 5
	translation "48:64=196:207"
}

actor TangoGreenSparkleSpawner :TangoWhiteSparkleSpawner 29403
{
	//$Category "Supercharge Special Effects"
	//$Color 5
	translation "48:64=112:127"
}

actor TangoRedSparkleSpawner : TangoWhiteSparkleSpawner 29404
{
	//$Category "Supercharge Special Effects"
	//$Color 5
	translation "48:64=176:191"
}

actor TangoOrangeSparkleSpawner: TangoWhiteSparkleSpawner 29405
{
	//$Category "Supercharge Special Effects"
	//$Color 5
	translation "48:64=214:223"
}

actor TangoPurpleSparkleSpawner: TangoWhiteSparkleSpawner 29406
{
	//$Category "Supercharge Special Effects"
	//$Color 5
	translation "48:64=250:254"
}
